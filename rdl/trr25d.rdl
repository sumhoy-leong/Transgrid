%*** TRR25D.RDL
%***
%*** Copyright (c) 1991, Mincom Pty Ltd, Brisbane 4120, Australia.
%*** All rights reserved.
%***
%*** 19-Sep-12 a9is6175     MO14 (Version 0001)
%*** ...................... Reset version number to 0001 for E8 upgrade. 
%***    **  NOTE:  The following RDL programs are very similar so any
%***    **         changes made to this code should also be made to them:
%***    **         MSB25H, MSBQFF, MSBQFT, MSBQQF and MSBQQT
%***    **
%***    **           /\        /\        /\        /\
%***                /  \      /  \      /  \      /  \
%***                 ||        ||        ||        ||
%***                 ||        ||        ||        ||
%***
%*** Revision History ***
%*** 14-Nov-05 E Fredeluces P05110870
%***                        Fix compilation errors in Ellipse 5.2.3.7.
%*** 20-Aug-02 E Fredeluces WCHG004650 - redundant replaced by TRB25S.
%*** 24-Jun-02 E Fredeluces WCHG003612
%*** ...................... To call ESR255 to accumulate the quantity
%*** ...................... instead of ESR25A.
%*** ...................... To print a blank supplier number if the 
%*** ...................... Supplier No. parameter is spaces.
%*** 04-Jun-02 E Fredeluces WCHG002805
%*** ...................... Read the MSF175 and determine the usage using 
%*** ...................... the accounting period.  This needs to be checked
%*** ...................... If the Usage parameter is set to "Y".
%*** ...................... Remove redundant request-parameters.
%*** ...................... Read the MSF180 prior to reading MSF175 table.      
%*** ...................... The MSF175 table is not reliable.  The usage
%*** ...................... will be picked-up from the MSF900 using the
%*** ...................... MSFX95 cross-reference file.  The program is
%*** ...................... split into two.  ESR25A will generate the report.
%*** 01-Jul-99 E Fredeluces W99070010
%*** ....................   Report not printing details.
%*** 22-Jan-99 D DIOLASO    Added Rec-type as a parameter
%*** ....................
%*** 02-Nov-98 D DIOLASO    Fix for MIMS 4.3
%*** ....................  
%*** 16-Apr-96 Mincom        Replace 'RC' table access with HRGET macro.
%*** ....................
%*** 15-Sep-92 Mary_Wachman Changes for ECNSW as follows:
%*** .....................  Change heading order. 
%*** 11-Sep-92 Mary_Wachman Changes for ECNSW as follows:
%*** .....................  Re-realign report heading and footings. 
%*** 10-Sep-92 Mary_Wachman Changes for ECNSW as follows:
%*** .....................  Realign report heading and footings. 
%*** 9-Sep-92 Mary_Wachman Changes for ECNSW as follows:
%*** ....................  Change heading order. Omit some heading fields and
%*** ....................  add page footing and report summary.
%*** 13-Feb-92 David_Gatward Changes for ECNSW as follows:
%*** .....................  change wording in headings to:
%*UOI  Estimated F  D Normal |Cntry |Unit of| Unit of | Trade   |Std | Lead
%*     Usage-FPA R  E Ord Qty|Origin| Purch |Purch eg.| Disc. % |Pack| Time
%*     Duration  T  L        |      | Price | EA,PKT  |If Applic|    |(days)
%*** 03-Oct-91 Shaun      C44546 MINC1 ---
%*** .................... Allow up to 50 lines of FPA header and supplier
%*** .................... descriptions.
%*** 15-Oct-91 Shaun      C44546 Version 003A. 3.008 to 3.008E.
%*** 20-Jun-91 Barbara    CSB253 MINC1 ---
%*** .................... Change request params to accept mediums and check for
%*** .................... medium in mainfile selection.
%*** .................... When supplier is entered no selection on status etc
%*** .................... is required.
%*** .................... Standardised variables and layout to conform to new
%*** .................... standard and make  consistent with MSB25H and new
%*** .................... MSBQFF, QFT, QQF and QQT.
%*** 14-Jun-91 Barbara    CSB253 Version 003. 3.007 to 3.008/6.
%*** 03-Dec-90 Paul       CSO255 MINC1 --- CHANGE PROGRAM BECAUSE OF MSF251
%*** .................... FILE CHANGE TO MSF251-QTY-TEND-UOI
%*** 03-Dec-90 Paul       CSO255 Version 002. 3.005 to 3.006/3.
%*** 11-Sep-90 Barbara    Added 'F' to key for FPA text reads.
%*** 06-Sep-90 Barbara    Tidyed up page headings so they are corrrect for all
%*** .................... options.
%*** 27-Aug-90 Peters     C99555 Version 001A. 3.004 to 3.004A.
%*** 26-Jul-90 Chrisw     Include working MSS110
%*** 09-Jul-90 CHRISW     Initial coding.
%***                      Generated by Version 003 of NEWRDL.MAC
%***
%PROGRAM-ID
       TRR25D
%AUTHOR
       Mincom PTY LTD
%NARRATIVE
      **  Request for Quotation - FPA - Printed
      **
      **  Print a Request for Quote for an FPA for a selected supplier
      **  or all suppliers or for a blank RFQ with no supplier details.
      **  If a district level breakdown is required details for all
      **  districts are printed, otherwise only the global details
      **  appear. Details are only printed when the RFQ medium is one
      **  of those requested and the status is either 'P' 'R' or 'Q'.
      **
      **  NOTE:  The following RDL programs are very similar so any
      **         changes made to this code should also be made to them:
      **         MSFQFF, MSBQFT, MSB25H, MSBQQF and MSBQQT
      **
%VERSION
       0001
%MODULE
       3250
%FILES
       MFILE = MSF253
       LFILE = MSF010
       LFILE = MSF200
       LFILE = MSF250
       LFILE = MSF251
       LFILE = MSF253
       LFILE = MSF900
       AFILE = MSFX96A
       AFILE = MSF251
       OFILE = TRO25DA
%ROUTINES
       MSSDAT
%SELECTION
<IF %SUP% <> SPACES>
       WHERE    (FPA-REC-TYPE    = 'F',   -
                 FPA-GROUP       = %FPA%, -
                 FPA-AGREEMENT   = SPACES,-
                 SUPPLIER-NO     = %SUP%, -
                 SUPPLIER-OFFER >= SPACES)
       REJECTIF (SUPPLIER-OFFER <> SPACES)
<ELSE>
       WHERE    (FPA-REC-TYPE    = 'F',   -
                 FPA-GROUP       = %FPA%, -
                 FPA-AGREEMENT   = SPACES,-
                 SUPPLIER-NO     = NOSUP, -
                 SUPPLIER-OFFER >= SPACES)
       REJECTIF (SUPPLIER-OFFER <> SPACES)
       REJECTIF (STATUS-253     <> 'P', -
                 STATUS-253     <> 'R', -
                 STATUS-253     <> 'Q')
<ENDIF>
%OUTPUT-FILE
       A
%CONTROL
       RECORD-LENGTH = 96 
       COPY-REQUEST  = ESR255
%DETAIL
<IF %USAGE% ='Y'>
  <IF QTY <> 0>
{LP }{CP }{STRDTE}{CURDTE}{DC}FFFFF{FI}{STK    }{SUP }{QTY        }{PR}{TR    }T{TRDATE}{PS}{PE}
  <ENDIF>
<ELSE>
{LP }{CP }{STRDTE}{CURDTE}{DC}FFFFF{FI}{STK    }{SUP }{QTY        }BBBBBBBBBBBBBBBBBBBBBBBBBBBBB
<ENDIF>
%REQUEST-PARAMETERS
          FPA     PIC (X(5))  TEXT  'FPA Group                           :'
          SUP     PIC (X(6))  TEXT  'Supplier Number                     :'
          DBD     PIC (X(1))  TEXT  'District Breakdown Required   (Y/N) :' 
          BLANK   PIC (X(1))  TEXT  'Blank RFQ Required            (Y/N) :'
          USAGE   PIC (X(1))  TEXT  'Exclude No Usage Over 24 Mths (Y/N) :'
%VARIABLES
%* Constants
       B       = ' '    CONSTANT
       L1      = 'Item' CONSTANT
       L       = 'For'  CONSTANT
       Z       = '|'    CONSTANT
       F       = %FPA%  CONSTANT
       CURDTE  = DATE() CONSTANT
       SUPL    = SUPPLIER-NO
%*
%*  Get the first supplier-no if the %SUP% is blank
%*
       NOSUP   = SUPPLIER-NO OF MSF253             -
                 WHERE    (FPA-REC-TYPE  = 'F',    -
                           FPA-GROUP     = %FPA%,  -
                           FPA-AGREEMENT = SPACES, - 
                           SUPPLIER-NO  >= SPACES) -
                 REJECTIF (SUPPLIER-NO   = SPACES) -
                 MAX      (1)       CONSTANT
%*
%* Determine Supplier Number
%*
       SUP     = CHOOSE (SUPL IF (%SUP% <> SPACES), NOSUP)
%*
%*  FPA Item File
%*
       FI      = FPA-ITEM-NO FROM MSF251 -
                 WHERE    (FPA-REC-TYPE  = 'F',    -
                           FPA-GROUP     = %FPA%,  -
                           FPA-ITEM-NO  >= SPACES) -
                 REJECTIF (DSTRCT-CODE   = SPACES)
       STK     = STOCK-CODE        VIA (FI)
       DC      = DSTRCT-CODE       VIA (FI)
%*
%* Determine Previous 24 Months
%*
       CP      = WX-STORES-CP              FORMAT (PERIOD) CONSTANT
       LP      = PERIOD-YRMN OF MSSDAT     -
                 WHERE (OPTION      = 'J', -
                        PERIOD-YRMN = CP,  -
                        DAYS        = -24) FORMAT (PERIOD) CONSTANT
       LPYR    = SUBSTR(LP,1,2)                            CONSTANT
       LPCY    = CHOOSE ('20' IF (LPYR < '25'), '19')      CONSTANT
       PRD     = CATENATE (LPCY,LP)                        CONSTANT
%*
%* Get the Ending Date of the previous 25th month 
%*
       LP25    = PERIOD-YRMN OF MSSDAT     -
                 WHERE (OPTION      = 'J', -
                        PERIOD-YRMN =  LP, -
                        DAYS        = -1)                  CONSTANT
       EDATE   = ENDING-DATE-1-9 VIA (LP25)                CONSTANT
%*
%* Determine Start Date of the Accounting Period
%*
      STRDTE   = DATE2 OF MSSDAT              -
                 WHERE (OPTION      = '1',    -
                        DATE1       = EDATE,  -
                        DAYS        = 1)     FORMAT (DATE) CONSTANT
%*
%* Get Process Date of the Starting Date
%*
   PS1    = DAYS OF MSSDAT -
            WHERE (OPTION  = '8',-
                   DATE1-X = STRDTE)            CONSTANT
   PS11   = CALC (PS1 * 1) I-PIC (9(06))        CONSTANT
   PS     = SUBSTR (PS11,3,4)                   CONSTANT
%*
%* Get Process Date of the Ending Date
%*
   PE1    = DAYS OF MSSDAT -
            WHERE (OPTION  = '8',-
                   DATE1-X = CURDTE)            CONSTANT
   PE11   = CALC (PE1 * 1) I-PIC (9(06))        CONSTANT
   PE     = SUBSTR (PE11,3,4)                   CONSTANT
%*
%* Use the Cross-Reference file to access MSF900
%*
   MSFX96 = STOCK-CODE-1 FROM MSFX96A       -
             WHERE    (STOCK-CODE-1   = STK,-
                       DSTRCT-CODE-1  = DC, -
                       REC900-TYPE-1  = 'S',-
                       PROCESS-DATE  >= PS, -
                       PROCESS-DATE  <= PE) -
             REJECTIF (REC900-TYPE-1 <> 'S')-
             IF %USAGE% = 'Y'
 
   T      = REC900-TYPE-1  VIA (MSFX96)
   PR     = PROCESS-DATE   VIA (MSFX96)
   TR     = TRANSACTION-NO VIA (MSFX96)
   USR    = USERNO-1       VIA (MSFX96)
%*
%* Determine usage in MSF900
%*
   MSF900  = DSTRCT-CODE OF MSF900             -
             WHERE    (DSTRCT-CODE     = DC,   -
                       PROCESS-DATE    = PR,   -
                       TRANSACTION-NO  = TR,   -
                       USERNO          = USR,  -
                       REC900-TYPE    >= T,    -
                       REC900-TYPE    <= T)    -
             SELECTIF (TRAN-TYPE       = 'ISS')-
             SELECTIF (TRAN-TYPE       = 'ISI')-
             MAX      (1)                      -
             IF %USAGE% = 'Y' 

   TRDATE  = TRNDTE-REVSD   VIA (MSF900) FORMAT (REVERSE-DATE)
   QTY     = QUANTITY-ISS-S VIA (MSF900) D-PIC S999999999.9999
%*
%*                           End of Program
%* 

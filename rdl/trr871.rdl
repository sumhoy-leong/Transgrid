%***  $Id: trr871.rdl,v 3.0 2008/06/20 15:15:00 Exp $
%*** TRR871.rdl
%***
%*** Copyright (c) 1998, Mincom Pty Ltd, Brisbane 4120, Australia.
%*** All rights reserved.
%***
%*** Generated by version 2.000h of ReportLink on 24-Feb-1998 at 12:25:00
%***
%*** Generated for version 3.041 of MIMS
%***
%*
%* REVISION HISTORY
%*
%*    20-June-2008 E Fredeluces SC1892259
%*                              Amend the parameter "Last Day To Report" default
%*                              value from 14 to 5 days prior to current date.
%*
%*    11-Jul-2005  Lubanovic   P05060270
%*                             added new code so that we append the whouse id
%*                             to the purchase req number if the district
%*                             is 'GRID'. 
%* 
%*    28-Jul-2003  R Yeung     WCHG023914
%*                             Add STOCK-CODE in Mail for 'IR' from MSF141.
%*    24-Jul-2003  R Yeung     WCHG023914
%*                             Add DSTRCT-CODE, STOCK-CODE for 'IR', STOCK-CODE for 'PR'
%*                             into the report.
%*
%*    13-Jan-2003  Amanda N.   WCHG012342
%*                             New Enhancement
%*                             - Add new request parameters to Exclude 5 position ID.
%*
%*    15 Feb 2002  D Diolaso   WCHG001230
%*                             Further changes required by Stuart James:
%*                             a) Employee ID of the PR requestor be reflected
%*                                in the report.
%*                             b) REASON reflected in the report should be 
%*                                'Confirmation only, Order placed' when and 
%*                                order has been placed for the purchase req.
%*                             c) No e-mail should be sent with respect to
%*                                purchase requisition in which an order has 
%*                                been placed.
%*
%*    14 Feb 2002  D Diolaso   WCHG001230
%*                             Inclusion of option to send (or not to send)
%*                             email notification.   
%*                             Exclusion of position id 'QUOTE' from email
%*                             notification.
%*                             Suppress email notification when no authorising
%*                             officer is assigned to the PR.
%*
%*    12 Feb 2002  D Diolaso   WCHG001230 
%*                             Automatically sends an e-mail to the requesting
%*                             officer of the requisition for authorisation.
%*
%*    07 Feb 2002  D Diolaso   WCHG001148
%*                             a) Inclusion of Purchase Requisition
%*                             b) Will default 'last date of report' to 
%*                                14 days prior to the current when the
%*                                parameter date is spaces.
%*                             
%* Define Report Parameters
%*
%* Report Name               :  TRR871
%*
%* Reports Produced          :  A
%*
%* Report Description        :  TRANSACTIONS REQUIRING AUTHORISATION
%*                               Cloned from MSR871 originally 
%* Parameters
%*     Parameter Number      :  1
%*     Row Number            :  1
%*     Description           :  
%*     Length                :  10
%*     Optional
%*     Parameter Number      :  2
%*     Row Number            :  2
%*     Description           :  
%*     Length                :  8
%*
%*
%* End Report Parameter Definition
%*
%*
%* Request Header
%*
%*     Description Line 1    :  
%*     Description Line 2    :  
%*     Override Program      :  
%*     Number of Requests    :  0
%*     Reverse Option        :  TRUE
%*     Start Option          :  TRUE
%*     District Code Option  :  FALSE
%*     Medium/Printer        :  TRUE
%*     Request Record        :  TRUE
%*     Number to Process     :  
%*
%* End Request Header
%*
%PROGRAM-ID
    TRR871
%AUTHOR
    Mincom Pty Ltd
%NARRATIVE
    report Transactions requiring authorisation 
%MODULE
    3870
%VERSION
    003 
%FILES
    MFILE = MSF877
    AFILE = MSF140
%* DD0207 WCHG001148 - Added auxilliary file MSF230
    AFILE = MSF230
%* DD0215 WCH001148  - Added look-up file MSF231
    LFILE = MSF231
    LFILE = MSF141
    LFILE = MSF620
    LFILE = MSF870
    LFILE = MSF878
    LFILE = MSF12A
    RFILE = TRR871A
    OFILE = TRO871B
%ROUTINES
    MSSEMP
    MSSDAT 
%CONTROL
    COPY-REQUEST = TRB871
%SELECTION
<IF %POSN% = SPACES>
    WHERE (AUTHSD-STATUS          = 'U', -
           POSITION-ID           >= SPACES)
    REJECTIF (%POSID1% <> SPACES, POSITION-ID = %POSID1%)
    REJECTIF (%POSID2% <> SPACES, POSITION-ID = %POSID2%)
    REJECTIF (%POSID3% <> SPACES, POSITION-ID = %POSID3%)
    REJECTIF (%POSID4% <> SPACES, POSITION-ID = %POSID4%)
    REJECTIF (%POSID5% <> SPACES, POSITION-ID = %POSID5%)
<ELSE>
    WHERE (AUTHSD-STATUS          = 'U', -
           POSITION-ID            = %POSN%, -
           TRAN-877-TYPE         >= SPACES)
<ENDIF>
%ORDER
    POSITION-ID
%REPORT
    A
%TITLE
    'TRANSACTIONS REQUIRING AUTHORISATION'
%PAGE HEADING
'*** Last Date To Report : ' {RDATE }

'Trans                                  Requested by               Created      Required   Priority        Reason'
'type   ID        StockCode  District   EmpID & Name                  on           by'

%DETAIL
<IF A06 > '          ' AND A = 'IR'>
'Directed to' {A00       } {A01                         }':-' {E00     } {E01                                   }
AA {TRN    }      {SC     }  {A0}  {A19} {A03                    } {A16      }  {A04      }  BBBB   {A05                           }

<ENDIF>
<IF B06 > '          ' AND A = 'PR'>
'Directed to' {A00       } {A01                         }':-' {E00     } {E01                                   }
AA {TRN    }      {SC     }  {A0}  {B19} {A03                    } {B16      }  {A04      }  BBBB   {A05                           }

<ENDIF>
%REPORT-SUMMARY


{UNDERLINE                                                                                                                         }
                        
                                                           '*** End of Report ***'
%OUTPUT-FILE
  B
%CONTROL
    RECORD-LENGTH = 61 
%DETAIL
<IF C06 > '          ' AND %EMAIL% = 'Y' AND A00 <> 'QUOTE'>         
AA{A0}{TRN    }{C06     }{C16      }{E00     }{D01 }{SC     }
<ENDIF>
%REQUEST-PARAMETERS
  POSN   PIC (X(10)) TEXT 'Position ID         (Blank for All)                    : '
  LSTDTE PIC (X(8))  TEXT 'Last Date To Report (Defaults to Today's Date if blank): '
  DAYP   PIC (9(3))  TEXT 'No. of Days Prior to Last Date To Report               : '
  EMAIL  PIC (X(1))  TEXT 'With Email Notification? (Y/N)                         : '
  POSID1 PIC (X(10)) TEXT 'Exclude Position Numbers                               : '
  POSID2 PIC (X(10)) TEXT
  POSID3 PIC (X(10)) TEXT
  POSID4 PIC (X(10)) TEXT
  POSID5 PIC (X(10)) TEXT

%VARIABLES
%* DD0207 WCHG001148 - Added TODATE and LDATE variables
    TODATE = DATE ()          CONSTANT
    DAYS   = %DAYP%           CONSTANT
    SDAYS  = CALC (DAYS * -1) CONSTANT
    DATE1  = CHOOSE (%LSTDTE% IF (%LSTDTE% <> SPACES), TODATE)
    LDATE  = DATE2 OF MSSDAT         -
             WHERE  (OPTION = '1',   -
                     DATE1  = DATE1, -
                     DAYS   = SDAYS) 
    RDATE  = LDATE FORMAT (DATE)
    A02 = TRANSACTION-KEY
    A0  = DSTRCT-CODE
    A00 = POSITION-ID
    A   = TRAN-877-TYPE
%*               A File 
    A06 = REQUESTED-BY FROM MSF140 -
    WHERE (DSTRCT-CODE            = DSTRCT-CODE, -
           IREQ-NO               >= TRANSACTION-KEY) -
           REJECTIF (CREATION-DATE > LDATE) -           
           STOPIF   (IREQ-NO > TRANSACTION-KEY) 
    A10 = PRIORITY-CODE VIA (A06)
    A13 = REQ-BY-DATE VIA (A06)
    A17 = CREATION-DATE VIA (A06)
    A16 = GET (DISPLAY-DATE FROM (A17 ))
    A18 = REQUESTED-BY VIA (A06)
    A19 = SUBSTR (A18,6,5)
    A11 = IREQ-NO VIA (A06)
%* Read MSF141
    SC = STOCK-CODE OF MSF141 -
    WHERE (DSTRCT-CODE            = DSTRCT-CODE, -
           IREQ-NO                = A11, -
           IREQ-ITEM             >= SPACES) -
    MAX (1)

%*
%* DD0207 WCHG001148 - Added the following MSF230 access, B10, B13, B17 and B16
    B06  = REQUESTED-BY FROM MSF230 -
    WHERE (DSTRCT-CODE           = DSTRCT-CODE, -
           PREQ-NO              >= TRANSACTION-KEY) - 
           REJECTIF (CREATION-DATE > LDATE) -
           STOPIF   (PREQ-NO       > TRANSACTION-KEY)
    B10  = PRIORITY-CODE VIA (B06)
    B13  = REQ-BY-DATE VIA (B06)
    B17  = CREATION-DATE VIA (B06)
    B16  = GET (DISPLAY-DATE FROM (B17 ))
    B18  = REQUESTED-BY VIA (B06)
    B19  = SUBSTR (B18,6,5)

    C06  = CHOOSE (A06 IF (A = 'IR'), -
                   B06 IF (A = 'PR'), SPACES)
    C16  = CHOOSE (A16 IF (A = 'IR'), -
                   B16 IF (A = 'PR'), SPACES)
%*  C18  = CHOOSE (A18 IF (A = 'IR'), -
%*                 B18 IF (A = 'PR'), SPACES)

    D01  = PO-NO OF MSF231 -        
           WHERE (DSTRCT-CODE      = DSTRCT-CODE, -
                  PREQ-NO          = TRANSACTION-KEY, -
                  PREQ-ITEM-NO    >= SPACES) -
           MAX (1)

    WH   = WHOUSE-ID via (D01)
    WHID = substr (WH,1,2)
    TK   = substr (A02,1,6)
    TRWH = catenate (TK,'-',WHID)  D-PIC (X(9))
    TR   = catenate (TK,'   ')     D-PIC (X(9))
    TRN  = choose (TRWH IF (A0 = 'GRID', A = 'PR'), -
                   TR)  D-PIC (X(9))

    A05 = CHOOSE('Value above Limit      ' -
                              IF (REASON-CODE = '01' AND D01 = SPACES ) ,-
                 'Stock Code Restricted  ' -
                              IF (REASON-CODE = '02' AND D01 = SPACES ) ,-
                 'Account Code Restricted' -
                              IF (REASON-CODE = '03' AND D01 = SPACES ) ,-
                 'Loan Requested         ' -
                              IF (REASON-CODE = '04' AND D01 = SPACES ) ,-
                 'Over Entitlement       ' -
                              IF (REASON-CODE = '05' AND D01 = SPACES ) ,-
                 'No Category Access     ' -
                              IF (REASON-CODE = '06' AND D01 = SPACES ) ,-
                 'Requisition Incomplete ' -
                              IF (REASON-CODE = '07' AND D01 = SPACES ) ,-
                 'Forced Await Authority ' -
                              IF (REASON-CODE = '08' AND D01 = SPACES ) ,-
                 'Referred for Approval  ' -
                              IF (REASON-CODE = '09' AND D01 = SPACES ) ,-
                 'Modified Authorised By ' -
                              IF (REASON-CODE = '10' AND D01 = SPACES ) ,-
                 'Confirmation Only, Order Placed ' -
                              IF (D01 > SPACES), ' ')

    A12 = ORIG-PRIORITY OF MSF620 -
    WHERE (DSTRCT-CODE            = DSTRCT-CODE, -
           WORK-ORDER             = TRANSACTION-KEY)
    A15 = REQ-BY-DATE VIA (A12)


%* DD0207 WCHG001148 - Changed %LASTDATE% to LDATE to the rest of the lines
    A01 = POS-TITLE OF MSF870 -
    WHERE (POSITION-ID            = POSITION-ID)
    E00          = EMPLOYEE-ID OF MSF878 -
                   WHERE (POSITION-ID = A00, -
                          EMPLOYEE-ID >= SPACES) -
                   REJECTIF (POS-STOP-DATE < LDATE,      -
                             POS-STOP-DATE > '00000000') -
                   MAX (1)
    E01 = FORMATTED-NAME OF MSSEMP -
    WHERE (EMP-REC-TYPE           = '0', -
           EMPLOYEE-ID            = E00, -
           SECURITY-LEVEL         = '0', -
           COMMAREA-IND           = '0')

%* DD0207 WCHG001148 - Inclusion of 'PR' testing to applicable lines
    A09 = CHOOSE(A13  IF (TRAN-877-TYPE = 'IR' ) ,B13  IF (TRAN-877-TYPE = 'PR' ) ,A15  IF (TRAN-877-TYPE = 'WO' ) , SPACES )
    B   = CHOOSE(A10  IF (TRAN-877-TYPE = 'IR' ) ,B10  IF (TRAN-877-TYPE = 'PR' ) ,A12  IF (TRAN-877-TYPE = 'WO' ) , SPACES )
    A04 = GET (DISPLAY-DATE FROM (A09 ))
    AB06 = CHOOSE (A06 IF (A = 'IR'), B06 IF (A = 'PR'), SPACES)
    A08 = SUPP-CUST-NAME-1 OF MSF12A -
    WHERE (SUPPLY-CUST-ID         = AB06)

%* DD0207 WCHG001148 - Added IF A = 'IR' condition
    A07 = FORMATTED-NAME OF MSSEMP -
    WHERE (EMP-REC-TYPE           = '0', -
           EMPLOYEE-ID            = A06, -
           SECURITY-LEVEL         = '0', -
           COMMAREA-IND           = '0') IF A = 'IR'

%* DD0207 WCHG001148 - Added B07 and the rest
    B07 = FORMATTED-NAME OF MSSEMP -
    WHERE (EMP-REC-TYPE           = '0', -
           EMPLOYEE-ID            = B06, -
           SECURITY-LEVEL         = '0', -
           COMMAREA-IND           = '0') IF A = 'PR'

%* DD0207 WCHG001148 - Modification of A03 statement to include test of
%*                     IR and PR 
    A03 = CHOOSE(A07  IF (A08 = SPACES, -
                          A   = 'IR' ), -
                 B07  IF (A08 = SPACES, -
                          A   = 'PR' ) , A08 )

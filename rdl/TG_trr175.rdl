%***  $Id: trr175.rdl,v1.0 23/04/2012 16:00:00 Exp $
%*** TRR175.RDL
%***
%**  Copyright (c) 1999, Mincom Pty Ltd, Brisbane 4120, Australia.
%*** All rights reserved.
%***
%*** Revision History ***
%*** 18-Sep-12 N Perry       MO164 ---
%*** ......................  Changed getting WHOUSE to be from MSF1CS
%*** ......................  and SOH from MSF1HD
%*** 27-Jul-12 N Perry       MO164 (Version 0001)
%*** ......................  Removed 4 unreferenced variables.       
%*** ......................  Reset version to 0001 to Ellipse 8 format.
%*** ......................  Changed program to get SOH using MSSSOH 
%*** ......................  as SOH field is no longer exist in MSF180 now.
%*** 16-Apr-2012  E Fredeluces  SC3932131
%***                            Include the warehouse from the report.
%*** 25-Mar-1999 JN             W903240010
%***                            Put 'Other' Column in report - as catchall
%***                            Printed "other" items with values
%PROGRAM-ID
       TRR175
%AUTHOR
       Mincom PTY LTD
%NARRATIVE
      ***
      *** This program produces Inventory Dissection Summary Report
      *** for Stock type 'C','G','S','T'
      ***
      *** Initial Coding : Rakesh Kumar Mahajan (W811200016)
      ***
%FILES
       MFILE = MSF170
%*       AFILE = MSF180
       AFILE = MSF1CSA
	   AFILE = MSF1HDA
       LFILE = MSF183
       LFILE = MSF194
       RFILE = TRR175A
%VERSION
      0001
%ROUTINES
      MSSSOH
%SELECTION
<IF %DC% <> SPACES>
      WHERE  (DSTRCT-CODE >= %DC%,-
              DSTRCT-CODE <= %DC%)
<ELSE>
      WHERE  (DSTRCT-CODE >= SPACES)
<ENDIF>
%ORDER
      DSTRCT-CODE
      WH
%CONTROL-BREAKS 
      DSTRCT-CODE
      WH
%REPORT-TITLE
'Materials Management Information System'
%PAGE HEADING 
                                         'Inventory Dissection Summary Report'
{UNDERLINE                                                                                                                         }
 'Districts'

' Warehouse                     Capital Spares    System Spares     Trans Spares      General Lines        District TOTALS' 
'                       Other    Stock C - 545    Stock S - 562     Stock T - 546        Stock - G '

%DETAIL
<IF OAMT <> 0, WH-FLG = 'Y'>

'                        ( "Other" Stock item:'{SC     } {DST} {VAL         }')'
<ENDIF>
%BREAK WH FOOTING
<IF WH-FLG = 'Y', TDAMT <> 0>
   {WH}        {TOAMT       }   {TCAMT       }   {TSAMT       }   {TTAMT       }    {TGAMT       }        {TDAMT         }

<ENDIF>
%BREAK DSTRCT-CODE FOOTING
{DST}          {TOAMT       }   {TCAMT       }   {TSAMT       }   {TTAMT       }    {TGAMT       }        {TDAMT         }
{UNDERLINE                                                                                                                         }

%REPORT-SUMMARY
{UNDERLINE                                                                                                                         }

'Totals '      {TOAMT       }   {TCAMT       }   {TSAMT       }   {TTAMT       }    {TGAMT       }        {TDAMT         }

{UNDERLINE                                                                                                                         }

                                                    '*** End Of Report ***'
%REQUEST-PARAMETERS
       DC           D-PIC (X(4))   TEXT  'District Code (Blank for all): '
       WHID         D-PIC (X(4))   TEXT  'Whouse-Id     (Blank for all): '
%VARIABLES     
%* Main File
       DST    = DSTRCT-CODE
%*       A1     = INVENT-VALUE
       B1     = STOCK-TYPE
       SC     = STOCK-CODE
       MFLAG  = CATEG-MGT-FLG
%*       VAL170 = INVENT-VALUE
       PRC170 = INVENT-COST-PR
       CATSW  = CAT-COST-SW

        CUST-ID = CUSTODIAN-ID FROM MSF1CSA -
                  WHERE    (DSTRCT-CODE = DST,    -
                            WHOUSE-ID  >= SPACES) -
                  REJECTIF (WHOUSE-ID  <> %WHID%, -
                            %WHID%     <> SPACES) -
                  REJECTIF (CUSTODIAN-TYPE <> 'W')
					 
	    SOH180 = SOH FROM MSF1HDA -
		         WHERE   (CUSTODIAN-ID-1 = CUST-ID, -
				          BIN-CODE      >= SPACES)  -
		         SELECTIF (STK-OWNERSHP-IND = 'O')  -
				 SELECTIF (STK-OWNERSHP-IND = 'C')  -
				 REJECTIF (HOLDING-TYPE    <> 'F')  -
				 REJECTIF (STOCK-CODE      <> SC )

        WH180 = WHOUSE-ID VIA (CUST-ID)
        WH     = CHOOSE   (WH180 IF (WH180 <> SPACES), -
                           WH180)

        WH-FLG= CHOOSE   ('Y' IF (%WHID% = SPACES),  -
                          'Y' IF (WH     = %WHID%,   -
                                  %WHID% <> SPACES), -
                          'N')

%* Get all inventory category for each stock code...
       MSF183 = STOCK-CODE OF MSF183             -
                WHERE    (STOCK-CODE   = SC,     -
                          DSTRCT-CODE  = DST,    -
                          WHOUSE-ID    = WH180,  -
                          INVENT-CAT  >= SPACES) -
                REJECTIF (WHOUSE-ID    = SPACES) -
                MAX      (1)                     
%*                IF        MFLAG = 'Y'

       I      = INVENT-CAT  VIA (MSF183)
%*       INV-CAT= CHOOSE   (I IF (I <> SPACES), I)

%* Get inventory category value ...
%*
       MSF194 = INVENT-VALUE OF MSF194       -
                WHERE    (DSTRCT-CODE = DST, -
                          STOCK-CODE  = SC,  -
                          INVENT-CAT  = I)
%*                IF        MFLAG = 'Y'

       PRC194 = INVENT-COST-PR VIA (MSF194)

%*  Determine Inventory Cost Price to use

       PRC    = CHOOSE (PRC194 IF (MFLAG = 'Y', CATSW = 'Y'), -
                        PRC170)

%*  Calculate Invetory Value for Non-Category Managed item
       INVVAL = CALC (SOH180 * PRC)

       VAL    = CHOOSE (INVVAL IF (WH-FLG = 'Y'), 0) -
                    D-PIC (---,---,--9.99)
%* Other Stock Type
       OT     = CHOOSE (' ' IF (B1 = 'C'), -
                        ' ' IF (B1 = 'G'), -
                        ' ' IF (B1 = 'S'), -
                        ' ' IF (B1 = 'T'), -
                        'OTH')      

%* Get value for each stock type
    CAMT     = choose (VAL  IF ( B1 = 'C'), 0)    D-PIC (---,---,--9.99)
    GAMT     = choose (VAL  IF ( B1 = 'G'), 0)    D-PIC (---,---,--9.99) 
    SAMT     = choose (VAL  IF ( B1 = 'S'), 0)    D-PIC (---,---,--9.99)
    TAMT     = choose (VAL  IF ( B1 = 'T'), 0)    D-PIC (---,---,--9.99)
    OAMT     = choose (VAL  IF ( OT = 'OTH'), 0)    D-PIC (---,---,--9.99) 
%*        GT       = CALC (CAMT + GAMT + SAMT + TAMT)    D-PIC (---,---,--9.99) 

%* Totals for each stock type
    TCAMT     = TOTAL(CAMT)                D-PIC (---,---,--9.99) 
    TGAMT     = TOTAL(GAMT)                D-PIC (---,---,--9.99) 
    TSAMT     = TOTAL(SAMT)                D-PIC (---,---,--9.99)
    TTAMT     = TOTAL(TAMT)                D-PIC (---,---,--9.99)
    TOAMT     = TOTAL(OAMT)                D-PIC (---,---,--9.99)
    TDAMT    = TOTAL(VAL)                D-PIC (---,---,--9.99)
%*    TTDAMT   = CALC (TCAMT + TGAMT + TSAMT + TTAMT)    D-PIC (---,---,--9.99)
%* 
%* End OF Program      

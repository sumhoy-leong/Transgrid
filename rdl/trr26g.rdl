%*** TRR26G.rdl
%*** Revision History ***
%*** ----------------
%*** 28-Sep-12  a9jw6280  MO164 ---
%*** .................... Removed NO-STD-HEADINGS from %CONTROL 
%*** .................... and add %TITLE and %SUMMARY to 
%*** .................... allow report having standard header.
%*** 27-Jul-12  a9jw6280  MO164 (Version 0001)
%*** .................... Add HEADING in the BREAK control and changed the
%*** .................... PIC into D-PIC or I-PIC depends on the requirement.
%*** .................... Reset version number to 0001 for E8 upgrade.
%*** 14-Mar-12  GPresser  3897515                (Version 0020)
%*** .................... Only select 'O'wned Stock or 'P'urchase Req items
%*** 22-Feb-06  AIesho        P05110872
%*** .....................Append the %DISTRICT-CODE% code to STORE when
%*** .....................searching for a printer.  This is part of the
%*** .....................Ellipse Upgrade where every printer must be
%*** .....................unique. District code has to be before and after
%*** .....................the word STORE
%*** 28-Jun-05 Amanda N  P05060270
%*** .................... Modified program to cope with district consolidation
%*** .................... into GRID. Add warehouse id as request parameter
%*** .................... Add Warehouse id to tital when Warehouse id is 
%*** .................... selected.
%*** 30-Aug-04 RYeung    WCHG063519 - For Weekly, select all recs from
%*** ....................19950101 and status = 18. 
%*** 26-Aug-04 RYeung    WCHG063519 - Add in Invoice From and To date
%*** 31-Dec-02 RKMahajan WCHG012117 - Added new filters.
%*** 24-May-01 Casagrand Re-enabled dynamic printing and removed report C
%*** ................... as this report was causing runtime errors in Unix.
%*** 09-May-01 Casagrand Disabled dynamic printing functionality as was
%*** ................... causing runtime errors.
%*** 04-Nov-98 Brandon   Fix for MIMS 4.3
%*** 07-APR-98 NCHANDRA. W80190042.
%*** ....................Added MSF810 reference to obtain ACCOUNTANT names 
%*** ....................because the program was crashing on HRGET to
%*** ....................reference msf810 and also have changed the codes
%*** ....................to get district names in full via mss002 routine
%*** ....................to report all the districts with or without 
%*** ....................anything to report on , this was a problem because 
%*** ....................users could not get all the districts reported 
%*** ....................on any run.Removed any obsolete codes from "THIS"
%*** ....................program.
%*** ....................
%*** 16-Apr-96 Mincom    Replace 'RC' table access with HRGET macro.
%*** 16-NOV-95 Peter Lacey 
%*** ...................... Converted from 3.010 to 3.013. Changed field
%*** ...................... MSF260-AUTH-BY to MSF260-AUTHSD-BY.
%*** 27-Jul-95 Katherine_K. ISCS22841
%*** ...................... Added date of amendment of invoice
%*** 24-Jul-95 Katherine_K. ISCS 22841
%*** ...................... Added Accountant descriptor showing name and service
%*** ...................... number.
%*** 06-Jun-94 Mary_W ... Vers 001D  Corrected printer. 'N' for Head Office
%*** .................... Only printed at site.
%*** 17-May-94 Mary_W ... Vers 001C  Amend quantity invoiced to look at 
%*** .................... amended quantity if LAST-AMEND-NO > 0.
%*** 10-May-94 Mary_W ... Vers 001B  Amend to correct print of costing %.
%*** 09-May-94 David_G .. Vers 001B  Only report items with appr-stat='00'
%*** .................... items with pmt-stat='18' always have appr-stat='00'.
%*** 31-Jan-94 Mary_W ... Vers 001A  Initial Coding
%*** ....................
%*** 04-Apr-95 Assia .... Modified page heading description.
%*** ....................
%*** 11-Apr-95 Assia .... Modified report warning description.
%***
%PROGRAM-ID
       TRR26G
%AUTHOR
       Mary Wachman
%NARRATIVE
      **  Invoices Mismatched Due to Quantity.
      **  Reports invoice items with an item status of 18.
%* Change version number in variable 'ver' only.
%VERSION
       0001
%MODULE
       3260
%ROUTINES
        MSS232
        MSSDAT
%FILES
        MFILE = MSF000
        AFILE = MSF26AA
        LFILE = MSF000
        LFILE = MSF010
        LFILE = MSF100
        LFILE = MSF200
        LFILE = MSF220
        LFILE = MSF221
        LFILE = MSF230
        LFILE = MSF231
        LFILE = MSF260
        LFILE = MSF261
        LFILE = MSF810
        RFILE = TRR26GA
        RFILE = TRR26GB
%SELECTION
<IF %DISTRICT-CODE% = SPACES>
    WHERE    (DSTRCT-CODE      = SPACES, -
              CONTROL-REC-TYPE = 'AD', -
              CONTROL-REC-NO  >= SPACES)
    REJECTIF (DSTRCT-STATUS   <> 'A')
<ELSE>
    WHERE    (DSTRCT-CODE      = SPACES, -
              CONTROL-REC-TYPE = 'AD', -
              CONTROL-REC-NO  >= %DISTRICT-CODE%, -
              CONTROL-REC-NO  <= %DISTRICT-CODE%)
<ENDIF>
%ORDER
   ASC CONTROL-REC-NO
       SUPPLIER-NO           VIA (A)
       PO-NO                 VIA (A)
       PO-ITEM-NO            VIA (A)
       INV-NO                VIA (A)
       PURCH-REQ             VIA (H)
%BREAK-FIELDS
       CONTROL-REC-NO
       SUPPLIER-NO           VIA (A)
       PO-NO                 VIA (A)
       PO-ITEM-NO            VIA (A)
       INV-NO                VIA (A)	   
       PURCH-REQ             VIA (H)
%*=============================================================================
%REPORT
     A
%CONTROL
    TRIM-REPORT
%TITLE
       ''	   
%PAGE HEADING
<IF %REGONLY% <> 'Y'>
                                            'QUANTITY MISMATCH REPORT -' {OP-LIT   }
<IF %WHOUSE% <> SPACES>
                                                 {WHDESC                                             }
<ENDIF>

                             'EXEMPTION REPORT - INVOICES from ' {COFDTFR} ' TO ' {COFDTTO}                  

{UNDERLINE                                                                                                                         }
      'Clause 82 of the Expenditure Procedures requires acquittal action to be performed at least FIVE CLEAR WORKING DAYS'
      'prior to the date due for payment'
      'Claims on the following list should be acquitted or advice forwarded stating reasons for non-acquittal no later'
      'than NOON ON THE 20TH DAY OF THIS MONTH.   Advices should be forwarded to the Accounts Officer / Customer Service.'
      '-- Facsimile No. (02) 284 3222 or (02) 284 3456'
'**********************************************************************************************************************************'

'<------------------- ORDER INFORMATION ---------------------->   <--------------------- INVOICE INFORMATION --------------------->'
'Number/Item         Qty      Qty       Purch.     Stkcode/       Number          /Itm   Date    Quantity UOP     Value    Pmt Auth'
            'Approved Received UOP   Officer    DCPR No.                                                                  Stat Stat'
{UNDERLINE                                                                                                                         }
<ENDIF>
%DETAIL
%BREAK CONTROL-REC-NO HEADING ON NEW-PAGE RESET-PAGE-COUNT
%BREAK INV-NO VIA (A) 
%BREAK PURCH-REQ VIA (H) HEADING TEST-PAGE (4)
<IF %REGONLY% <> 'Y'>
 <IF PRINT = 'Y'>
  <IF WARNING <> 'N'>
'**********************************************************************************************************************************'
'*                *****     THIS REPORT HAS NOT BEEN FULLY REVIEWED BY ACCOUNTS OFFICER / CUSTOMER SERVICE *****                  *'
'**********************************************************************************************************************************'
  <ENDIF>
                                                                 'Accountant :' {ACC     } {ACCDES                              }
<IF POT = 'O'>
 {POWH   } GGG {QAP    } {QRP    } {UO} {PO      } {SC     }   JJJJJJJJJJJJJJJJJJJJ KKK {INVDTE}SSSSSSSSSS {IU} {VAL-INVD} {PS} {AS}
<ELSE>
 {POWH   } GGG {QAP    } {QRP    } {UO} {PO      } {PREQ} {PI} JJJJJJJJJJJJJJJJJJJJ KKK {INVDTE}SSSSSSSSSS {IU} {VAL-INVD} {PS} {AS}
<ENDIF>
<IF SETA > 0>
'Settlement Disc:'{SETVAL}  {ST} {SETDUE}
<ENDIF>
<IF ACCTNO1 <> SPACES>
'Account :' {ACCTNO1              }'Alloc %' {T1   } {WOP-LIT  } {WP1   } {EQP-LIT }{EQUIP1    }
<ENDIF>
<IF ACCTNO2 <> SPACES>
            {ACCTNO2              }'Alloc %' {T2   } {WOP-LIT  } {WP2   } {EQP-LIT }{EQUIP2    }
<ENDIF>
<IF ACCTNO3 <> SPACES>
            {ACCTNO3              }'Alloc %' {T3   } {WOP-LIT  } {WP3   } {EQP-LIT }{EQUIP3    }
<ENDIF>
<IF ACCTNO4 <> SPACES>
            {ACCTNO4              }'Alloc %' {T4   } {WOP-LIT  } {WP4   } {EQP-LIT }{EQUIP4    }
<ENDIF>
<IF ACCTNO5 <> SPACES>
            {ACCTNO5              }'Alloc %' {T5   } {WOP-LIT  } {WP5   } {EQP-LIT }{EQUIP5    }
<ENDIF>
<IF ACCTNO6 <> SPACES>
            {ACCTNO6              }'Alloc %' {T6   } {WOP-LIT  } {WP6   } {EQP-LIT }{EQUIP6    }
<ENDIF>
<IF ACCTNO7 <> SPACES>
            {ACCTNO7              }'Alloc %' {T7   } {WOP-LIT  } {WP7   } {EQP-LIT }{EQUIP7    }
<ENDIF>
'Order Description:' {DESC1                                     }'Date Changed' {M-DAT-D}                      
<IF %FULLDESC% = 'Y'>
<SUPPRESS>
                     {DESC2                                     }  
<SUPPRESS>
                     {DESC3                                     }  
<SUPPRESS>
                     {DESC4                                     }  
<SUPPRESS>
                     {DESC5                                     }  
<ENDIF>                                                                   
'Supplier :'EEEEEE   {SUPP-ORD-NAME                 }

 <ENDIF>
<ENDIF>
%SUMMARY	
<IF %REGONLY% <> 'Y'>
<ENDIF>
%REPORT
     B
%CONTROL
    TRIM-REPORT
%TITLE
       ' '		   
%PAGE HEADING
                                            'QUANTITY MISMATCH REPORT REGISTER -' {OP-LIT   }
<IF %WHOUSE% <> SPACES>
                                                 {WHDESC                                                 }
<ENDIF>

                             'EXEMPTION REPORT - INVOICES from ' {COFDTFR} ' TO ' {COFDTTO}                  

{UNDERLINE                                                                                                                         }
'Order     Itm Supplier Name                           Invoice              Itm     Value'

%BREAK CONTROL-REC-NO HEADING ON NEW-PAGE RESET-PAGE-COUNT
%DETAIL
<IF PRINT = 'Y'>
 {POWH   } GGG EEEEEE {SUPP-ORD-NAME                 } JJJJJJJJJJJJJJJJJJJJ KKK {VAL-INVD}     {PS} {AS}
{LINE                                                            }{LINE                                                            }
<ENDIF>
%BREAK CONTROL-REC-NO FOOTING 
<IF DST <> SPACES>

'total Number/Value of Service Invoice items reported :'{CNT    } {VAL            }

                                     '<<<<<<<<<<<<<<< End of the TRR26GB report >>>>>>>>>>>>>>>>'
<ENDIF>
%SUMMARY	
%REQUEST-PARAMETERS
    FROM-DTE PIC (X(8)) TEXT 'From Invoice Date                    :'
    TO-DTE   PIC (X(8)) TEXT 'To   Invoice Date                    :'
    DWMCYCLE   PIC (X(1)) TEXT 'Daily/Weekly/Monthly-for blank CutOff date:'
    WARNING    PIC (X(1)) TEXT 'Print with Warning?           Y/N         :'
    PAID       PIC (X(1)) TEXT 'Search Paid invoices also?    Y/N         :'
    SDONLY     PIC (X(1)) TEXT 'Search Settlement Discs Only? Y/N         :'
    FULLDESC   PIC (X(1)) TEXT 'Print 5 line description?     Y/N         :'
    REGONLY    PIC (X(1)) TEXT 'Print Register Only?          Y/N         :'
    SITEPRIN   PIC (X(1)) TEXT 'Print at site?                Y/N         :'
    WHOUSE     PIC (X(2)) TEXT 'Warehouse Code                            :'
    OWNPUR     PIC (X(1)) TEXT 'Owned Stock/Purchase Req      O/P         :'
%*
    DEFPRIN    LIKE PRINTER1   OF MSF080
    REQUESTBY  LIKE REQUEST-BY OF MSF080
%VARIABLES
%* Page heading Constants                                     
WHOUSE = %WHOUSE% CONSTANT
VER-NO = '0020' CONSTANT                               
VER0 = SC0020-REC OF MSF000 -                          
       WHERE (DSTRCT-CODE      = '    ', - 
              CONTROL-REC-TYPE = 'SC', -     
              CONTROL-REC-NO   = '0020')  CONSTANT                              
VER1 = substr (VER0,1,1)                  CONSTANT                      
VER2 = substr (VER0,2,4)                  CONSTANT                      
VER = catenate (VER1,'.',VER2,'.',VER-NO) CONSTANT                                    
PGNO1 = PAGE ()                                        
PGNO = choose (PGNO1 IF (PGNO1 <> 0), 0) I-PIC (9(5))  
REQUESTBY1 = %REQUESTBY%                  CONSTANT                      
L12 = 'Run on:'                           CONSTANT                               
L13 = 'Req.By:'                           CONSTANT                              

%*
%*  Other Constants
%*  **************************************************************************
%*
LINE  = '__________________________________________________________________'-
         CONSTANT
WARNING = %WARNING%                       CONSTANT    
FDTE    = %FROM-DTE%                      CONSTANT FORMAT (DATE)
TDTE    = %TO-DTE%                        CONSTANT FORMAT (DATE)
DWM     = %DWMCYCLE%                      CONSTANT
OP      = %OWNPUR%                        CONSTANT
%* W802190042 - added to pick up dstrcts from the main control file.
DST     = CONTROL-REC-NO
DSTNAME = get (DISTRICT-NAME FROM ('MSS002') DISTRICT (DST)
%*
%*
%* Calculate Daily/Weekly/Monthly date in case CutOff date is blank.
%* For Daily flag date from and to both will be YESTERDAY.
    TODAY   = DATE ()                     CONSTANT FORMAT (DATE)
    YESTDAY = DATE2 OF MSSDAT -
              WHERE (OPTION = '1', -
                     DATE1  = TODAY, -
                     DAYS   = -1)         CONSTANT FORMAT (DATE)
%* For Weekly flag date from = 7 days before today
%* and date to = YESTERDAY.
%*    W1      = DATE2 OF MSSDAT -
%*              WHERE (OPTION = '1', -
%*                     DATE1  = TODAY, -
%*                     DAYS   = -7)         CONSTANT FORMAT (DATE)
    W1   = '19950101' CONSTANT FORMAT (DATE)

%* For Monthly flag dates are last period start and end dates.
    CP1     = WX-STORES-CP                CONSTANT
    LP      = PERIOD-YRMN OF MSSDAT -
              WHERE (OPTION      = 'J', -
                     PERIOD-YRMN = CP1, -
                     DAYS        = -1)    CONSTANT
    STDATE  = STARTING-DATE-1-9  VIA (LP) CONSTANT FORMAT (DATE)
    ENDDATE = ENDING-DATE-1-9    VIA (LP) CONSTANT FORMAT (DATE)
%*
%* choose date FROM and TO based on the flag when dates are blank.
    COFDTFR  = choose (FDTE    IF (FDTE <> SPACES), -
                      YESTDAY IF (FDTE  = SPACES, DWM = 'D'), -
                      W1      IF (FDTE  = SPACES, DWM = 'W'), -
                      STDATE  IF (FDTE  = SPACES, DWM = 'M'), -
                      SPACES)
    COFDTTO  = choose (TDTE    IF (TDTE <> SPACES), -
                      YESTDAY IF (TDTE  = SPACES, DWM = 'D'), -
                      YESTDAY IF (TDTE  = SPACES, DWM = 'W'), -
                      ENDDATE IF (TDTE  = SPACES, DWM = 'M'), -
                      SPACES)
    OP-LIT   = choose('Owned Stock' IF (OP = 'O'), -
                      'Purch Req  ' IF (OP = 'P'), SPACES)

%*
%* MSF260 afile Invoice Item Details
A = AUTHSD-BY FROM MSF26AA -
    WHERE (DSTRCT-CODE-2 = CONTROL-REC-NO, -
           APPR-STATUS  >= SPACES) -
    REJECTIF (ITM-PMT-STATUS <> '18') -
    STOPIF   (%PAID% <> 'Y', APPR-STATUS > '00')
PS = ITM-PMT-STATUS VIA (A)
AS = APPR-STATUS VIA (A)
DC = DSTRCT-CODE VIA (A) 
LD = LOADED-DATE VIA (A) FORMAT (DATE)
C = MSB265-RUN-NO VIA (A)
E = SUPPLIER-NO VIA (A)
F = PO-NO VIA (A)
G = PO-ITEM-NO VIA (A)
ININV = INV-NO VIA (A)
K = INV-ITEM-NO VIA (A)
ORG-VAL = FOR-VAL-INVD VIA (A)
ORG-DISC = FOR-DISC-INVD VIA (A)
ORG-STAX = FOR-STAX-INVD VIA (A)
D = calc(ORG-VAL + ORG-DISC + ORG-STAX)
U = LAST-AMEND-NO-9 VIA (A)
M = FRT-DOCKET VIA (A)
N = RECEIVED-BY VIA (A)
O = ACCOUNT-CODE VIA (A)
P = PROJECT-NO VIA (A)
W = WORK-ORDER VIA (A)
Q = EQUIP-NO VIA (A)
S1 = QTY-INVOICED VIA (A) I-PIC (-(6)9.99)
IU = UNIT-OF-PURCH VIA (A)

%* Invoice Header Details 
B = ACCOUNTANT OF MSF260 WHERE (DSTRCT-CODE = DC,-
                                SUPPLIER-NO = E ,-
                                INV-NO      = ININV)
INVDTE    = INV-DATE VIA (B) FORMAT (DATE)
SIN       = SUP-INV-NO   VIA (B)
DDAT      = DUE-DATE     VIA (B) FORMAT (DATE)
OIN       = ORIG-INV-NO  VIA (B)
INVTYPE   = INV-TYPE VIA (B)
EXTINV    = EXT-INV-NO VIA (B)
J         = choose (EXTINV IF (EXTINV <> SPACES), ININV)

%* Invoice Amendment Details
AMD-VAL = FOR-AMD-VAL OF MSF261 WHERE (DSTRCT-CODE = DC,-
                                 SUPPLIER-NO = E,-
                                 INV-NO      = ININV,-
                                 INV-ITEM-NO = K,-
                                 AMENDMENT-NO = U)
M-DAT-F         = AMEND-DATE    VIA (AMD-VAL) 
M-DAT-D         =GET (DISPLAY-DATE FROM (M-DAT-F)) 
R = AMEND-QTY VIA (AMD-VAL) I-PIC (-(13)9.99)
AMD-DISC = FOR-AMD-DISC VIA (AMD-VAL)
AMD-STAX = FOR-AMD-STAX VIA (AMD-VAL)
L = calc (AMD-VAL + AMD-DISC + AMD-STAX)

VAL-INVD = choose (L IF (U > 0),D) D-PIC ($(9)9.99-)
 S       = choose (R IF (U > 0),S1) I-PIC (-(6)9.99)

%* Supplier Details
SUPP-ORD-NAME = SUPPLIER-NAME OF MSF200 WHERE (SUPPLIER-NO = E)
SUPP-PMT-NAME = PAYMENT-NAME VIA (SUPP-ORD-NAME)

%* Order Details
H = GROSS-PRICE-P OF MSF221 WHERE (PO-NO = F,-
                                   PO-ITEM-NO = G) D-PIC ($(10)9.9999-)
WHI    = WHOUSE-ID VIA (H)
WHFLG  = choose('N' IF(WHI <> WHOUSE, WHOUSE <> SPACES), 'Y')
WHID   = SUBSTR (WHI, 1, 2)
WH     = CATENATE (F, '-', WHID)
POWH   = CHOOSE (WH IF (DC = 'GRID'), -
                 F)
ITEM-TYPE = PO-ITEM-TYPE VIA (H)
OP-OK     = choose('Y' IF(ITEM-TYPE = OP), 'N')
%* Retrieve the Warehouse ID Description
   WHDESC     = TABLE-DESC OF MSF010         -
                WHERE   (TABLE-TYPE = 'WH', -
                         TABLE-CODE =  DC & WHOUSE)
I = VAL-APPR VIA (H) D-PIC ($(12)9.99-)
PREQ = PURCH-REQ VIA (H)
PI = PREQ-ITEM VIA (H)
POT = PO-ITEM-TYPE VIA (H)
SC = STOCK-CODE VIA (H)
QRI = QTY-RCV-DIR-I VIA (H)
UO  = UNIT-OF-PURCH VIA (H)
CF = CONV-FACTOR VIA (H)
QRP = CALC (QRI * CF)
QAP = APPR-QTY-UOP VIA (H)

%* Order Header Details
PO = PURCH-OFFICER OF MSF220 WHERE (PO-NO = F)

%* Settlement Discount
SETA = SETTLE-PC-A VIA (H)
SETDAYSA = SETTLE-DAYS-A VIA (H)
SETVAL = CALC (SETA / 100 * VAL-INVD) FORMAT (BWZ)
SETDUE1 = DATE2 OF MSSDAT WHERE (OPTION = '1', DATE1 = INVDTE, DAYS = SETDAYSA) FORMAT (DATE)
SETDUE = choose (SETDUE1 IF (SETA > 0), SPACES) FORMAT (DATE)
SETDISCTXT = choose ('Settlement Discount: $' IF (SETA > 0), SPACES)
SETDISCUL = choose ('-----------------------------------------------' IF (SETA > 0), SPACES)
ST = choose ('due' IF (SETA > 0), SPACES)

%* Stock Code Details
SCINAME = ITEM-NAME OF MSF100 WHERE (STOCK-CODE = SC)
SCDESC1 = DESC-LINE-1 VIA (SCINAME)
SCDESC2 = DESC-LINE-2 VIA (SCINAME)
SCDESC3 = DESC-LINE-3 VIA (SCINAME)
SCDESC4 = DESC-LINE-4 VIA (SCINAME)

SCEXT-DESC = GET (READ-TEXT FROM ('MSF096') CODE ('SC') -
             KEY (SC)) MAX (99)

%* Purchase Req Item Details
PRDESC1 = ITEM-DESC1 OF MSF231 WHERE (DSTRCT-CODE = DC,-
                                       PREQ-NO = PREQ,-
                                       PREQ-ITEM-NO = PI)
PRDESC2  = ITEM-DESC2 VIA (PRDESC1) 
PRDESC3  = ITEM-DESC3 VIA (PRDESC1) 
PRDESC4  = ITEM-DESC4 VIA (PRDESC1) 
PREXT-DESC = GET (READ-TEXT FROM ('MSF096') CODE ('PR') -
             KEY (DC,PREQ,PI)) MAX (99)

%* Description Details
DESC1 = choose (SCINAME IF (SCINAME <> SPACES), PRDESC1)
DESC2 = choose (SCDESC1 IF (SCDESC1 <> SPACES), PRDESC2)
DESC3 = choose (SCDESC2 IF (SCDESC2 <> SPACES), PRDESC3)
DESC4 = choose (SCDESC3 IF (SCDESC3 <> SPACES), PRDESC4)
DESC5 = choose (SCDESC4 IF (SCDESC4 <> SPACES), SPACES)

%* Purchase Req Header Details
REQBY = REQUESTED-BY OF MSF230 WHERE (DSTRCT-CODE = DC,-
                                          PREQ-NO = PREQ)
%* Invoice Commentary
INV-COMM  = GET (READ-TEXT FROM ('MSF096') CODE ('II') -
            KEY (DC,E,ININV)) MAX (99)
INVCOM    = SIZE (INV-COMM)
%*Accountant Description
         ACC = B
         ACCDES   = SURNAME OF MSF810 WHERE (EMPLOYEE-ID = ACC)

%* Accounts
ACCTNO1    = GL-ACCOUNT-1 OF MSS232 WHERE (OPTION = 'R',-
                                         DSTRCT-CODE = DC,-
                                         REQ-TYPE-IND = 'P',-
                                         REQ-NO      = PREQ,-
                                         PREQ-ITEM   = PI) 
SI-WO     = WORK-ORDER   VIA (ACCTNO1)
SI-PNO    = PROJECT-NO   VIA (ACCTNO1)
SI-ENO    = EQUIP-NO     VIA (ACCTNO1)
SI-APC    = ALLOC-PC     VIA (ACCTNO1)
T         = ALLOC-PC     VIA (ACCTNO1) I-PIC (ZZ9.99)
SIWOP     = choose(SI-WO IF (SI-WO <> SPACES),SI-PNO)
WOP-LIT   = choose('Work Order:' IF (SI-WO <> SPACES),-
                   'Project No:' IF (SI-PNO <> SPACES), SPACES)
EQP-LIT   = choose('Equip. No:'  IF (SI-ENO <> SPACES), SPACES)
ACCTNO2   = GL-ACCOUNT-2 VIA (ACCTNO1)
ACCTNO3   = GL-ACCOUNT-3 VIA (ACCTNO1)
ACCTNO4   = GL-ACCOUNT-4 VIA (ACCTNO1)
ACCTNO5   = GL-ACCOUNT-5 VIA (ACCTNO1)
ACCTNO6   = GL-ACCOUNT-6 VIA (ACCTNO1)
ACCTNO7   = GL-ACCOUNT-7 VIA (ACCTNO1)
WO1       = WORK-ORDER-1 VIA (ACCTNO1)
WO2       = WORK-ORDER-2 VIA (ACCTNO1)
WO3       = WORK-ORDER-3 VIA (ACCTNO1)
WO4       = WORK-ORDER-4 VIA (ACCTNO1)
WO5       = WORK-ORDER-5 VIA (ACCTNO1)
WO6       = WORK-ORDER-6 VIA (ACCTNO1)
WO7       = WORK-ORDER-7 VIA (ACCTNO1)
PNO1      = PROJECT-NO-1 VIA (ACCTNO1)
PNO2      = PROJECT-NO-2 VIA (ACCTNO1)
PNO3      = PROJECT-NO-3 VIA (ACCTNO1)
PNO4      = PROJECT-NO-4 VIA (ACCTNO1)
PNO5      = PROJECT-NO-5 VIA (ACCTNO1)
PNO6      = PROJECT-NO-6 VIA (ACCTNO1)
PNO7      = PROJECT-NO-7 VIA (ACCTNO1)
WP1       = choose( WO1  IF (WOP-LIT = 'Work Order:'),-
                    PNO1 IF (WOP-LIT = 'Project No:'), SPACES)
WP2       = choose( WO2  IF (WOP-LIT = 'Work Order:'),-
                    PNO2 IF (WOP-LIT = 'Project No:'), SPACES)
WP3       = choose( WO3  IF (WOP-LIT = 'Work Order:'),-
                    PNO3 IF (WOP-LIT = 'Project No:'), SPACES)
WP4       = choose( WO4  IF (WOP-LIT = 'Work Order:'),-
                    PNO4 IF (WOP-LIT = 'Project No:'), SPACES)
WP5       = choose( WO5  IF (WOP-LIT = 'Work Order:'),-
                    PNO5 IF (WOP-LIT = 'Project No:'), SPACES)
WP6       = choose( WO6  IF (WOP-LIT = 'Work Order:'),-
                    PNO6 IF (WOP-LIT = 'Project No:'), SPACES)
WP7       = choose( WO7  IF (WOP-LIT = 'Work Order:'),-
                    PNO7 IF (WOP-LIT = 'Project No:'), SPACES)
EQUIP1    = EQUIP-NO-1   VIA (ACCTNO1)
EQUIP2    = EQUIP-NO-2   VIA (ACCTNO1)
EQUIP3    = EQUIP-NO-3   VIA (ACCTNO1)
EQUIP4    = EQUIP-NO-4   VIA (ACCTNO1)
EQUIP5    = EQUIP-NO-5   VIA (ACCTNO1)
EQUIP6    = EQUIP-NO-6   VIA (ACCTNO1)
EQUIP7    = EQUIP-NO-7   VIA (ACCTNO1)
T1        = ALLOC-PC-1   VIA (ACCTNO1)
T2        = ALLOC-PC-2   VIA (ACCTNO1)
T3        = ALLOC-PC-3   VIA (ACCTNO1)
T4        = ALLOC-PC-4   VIA (ACCTNO1)
T5        = ALLOC-PC-5   VIA (ACCTNO1)
T6        = ALLOC-PC-6   VIA (ACCTNO1)
T7        = ALLOC-PC-7   VIA (ACCTNO1)

%* Display warning if order value < (val invoiced + previous approved)
%* ie. If warning < 0.
%*WARNING = CALC (H - VAL-INVD - I)

%* Dynamic Printer
DSTSTR  = catenate (DC, 'STORE',DC)  I-PIC (X(13))
PRINSTR = PRINTER OF MSF010 -
          WHERE (TABLE-TYPE = 'PR', -
                 TABLE-CODE = DSTSTR)
PRIN    = choose (PRINSTR IF (%SITEPRIN% <> 'N'), -
                  PRINSTR IF (%DEFPRIN% = SPACES), -
                  %DEFPRIN%)
PRINX   = PRINTER (PRIN)

%* totals:
PRINT = choose ('Y' IF (INVDTE >= COFDTFR, INVDTE <= COFDTTO, WHFLG = 'Y', -
                        ININV  <> SPACES,  SETVAL <> 0,   DST <> SPACES, OP-OK = 'Y'), -
                'Y' IF (INVDTE >= COFDTFR, INVDTE <= COFDTTO, WHFLG = 'Y', -
                        ININV  <> SPACES, %SDONLY% = 'N', DST <> SPACES, OP-OK = 'Y'), -
                'N')
CNT1    = choose (1 IF (PRINT = 'Y', DST <> SPACES), 0)
VAL1    = choose (VAL-INVD IF (PRINT = 'Y', DST <> SPACES), 0)
CNT     = total (CNT1)
VAL     = total (VAL1) D-PIC ($(12)9.99-)
%*
%*End Of Program.

%*** TRR256.RDL
%***
%*** Copyright (c) 1990, Mincom Pty Ltd, Brisbane 4120, Australia.
%*** All rights reserved.
%***
%*** Revision History ***
%*** 22-Apr-2013 a9is6175 Change report heading delimiter from pipe (|) to commas (,)
%*** .................... for CSV format. Added apostrophe for numeric with leading zeros. 
%*** 11-Apr-2013 a9nl6176 Change TRT256 to TRO256. Break Heading into some parts.
%*** .................... Break details into some lines.
%*** 08-Jan-2013 a9nl6176 Change report heading delimiter from commas (,) to pipe (|)
%*** 11-Sep-2012 a9hs2812 Convert from ellipse 5 to ellipse 8
%*** 22-Jun-05 L Nee      WCHG097505
%*** .................... Added extended text to report.
%*** 14-Jan-98 G Presser  MIMS 4.3 Fix
%*** .................... Allow for FPA-REC-TYPE
%*** 02-Nov-98 D. Diolaso Fix to MIMS 4.3
%*** ....................
%*** 29-Sep-98 Jude E     W809290040
%*** .................... To change the signatories.   
%*** 16-Apr-96 Mincom     Replace 'RC' table access with HRGET macro.
%*** ....................
%*** 09-DEC-92 DAVID_G    Changes for ECNSW as follows:
%*** .................... Lead time was only printing 2 digits even though
%*** .................... it was JJJ. Make JJJJ 'cos of pic s9(3).
%*** 04-OCT-91 DAVID_G    Changes for ECNSW as follows:
%*** .................... conv-fact(M) should print 9999.9999 not 999.9999
%*** 03-Dec-90 Paul       CSO255 MINC1 --- CHANGE THIS PROGRAM BECAUSE
%*** .................... MSF251-QTY-TEND-UOI NOW HAS 2 DECIMAL PLACES HOWEVER
%*** .................... THIS PROGRAM DOES NOT REQUIRE TO  PRINT THE DECIMALS
%*** .................... AS ONLY PURCHASE REQS HAVE DECIMALS
%*** 03-Dec-90 Paul       CSO255 Version 003. 3.005 to 3.006/3.
%*** 02-Oct-90 Rozie      C25651 Version 002. 3.004A to 3.005/7.
%*** ...................... Add Supplier Code & Offer on MSF080.
%*** ...................... Displaying items for Suppliers other than
%*** ...................... the first supplier for the FPA GROUP
%*** 28-Aug-90 Rozie      CSB256 Version 001A. 3.004 to 3.004A.
%*** ..................... Display PO (Purch-officer of msf252)
%*** 06-Jul-90 ROZIE      Initial coding.
%***                      Generated by Version 003 of NEWRDL.MAC
%***
%PROGRAM-ID
       TRR256
%AUTHOR
       Mincom PTY LTD
%NARRATIVE
      **  REPORTS DETAILS OF A FPA/AGREEMENT FOR A SUPPLIER
%VERSION
       0004
%MODULE
       3250
%FILES
        MFILE = MSF252
        LFILE = MSF100
        LFILE = MSF200
        LFILE = MSF250
        LFILE = MSF251
        LFILE = MSF810
        OFILE = TRO256A
%SELECTION
%*** Enter selection criteria for MSF252
       WHERE (FPA-REC-TYPE = %FRT%,          -
              FPA-ID       = %FPA% & %AGR% , -
              FPA-ITEM-NO >= SPACES)
       SELECTIF (SUPPLIER-NO = %SUPP%)
       REJECTIF (%OFFR% <> SPACES, SUPPLIER-OFFER <> %OFFR%)
%BREAK-FIELDS
    DUM
%OUTPUT-FILE
    A
%CONTROL
    RECORD-LENGTH = 648
%BREAK DUM HEADER
{HDG                                                                                            }-
{HDG1                                                                                              }-
{HDG2                                                                                               }-
{HDG3                                                                                                }-
{HDG4                                                                                        }
%DETAIL
{A  }N{B}N{FPA-DESCR                             }N{PO       }N{FRSN      }N{SECN      }N{SURN                        }N{E    }N-
O{P                             }ONO{Q                             }ONO{R                             }ON-
O{S                              }ON{SS}N{PC}N{EF      }N{EX      }N{F  }N{G       }NO{X                                     }ON-
O{STK-DESC1                             }ONO{STK-DESC2                             }ONO{STK-DESC3                             }ON-
O{STK-DESC4                             }ON{C}N{DL}N{Z            }N{RQ      }N{Y }N{H              }N{I    }N{J }N{K   }N{L }N-
{M          }N{MNEM  }N{PART                         }
%REQUEST-PARAMETERS
       FRT LIKE FPA-REC-TYPE  TEXT 'FPA Rec Type     '
       FPA LIKE FPA-GROUP     TEXT 'FPA Number       '
       AGR LIKE FPA-AGREEMENT TEXT 'Agreement Number '
       SUPP PIC (X(6))        TEXT 'Supplier Code    '
       OFFR PIC (X(2))        TEXT 'Supplier Offer   '
%VARIABLES
       HDG    = 'FPA NO,AGREEMENT NO,AGREEMENT DESCRIPTION,PURCHASING OFFICER ID,FIRST NAME,SECOND NAME,SURNAME,'      CONSTANT
       HDG1   = 'SUPPLIER NO,SUPPLIER NAME,SUPPLIER ADDRESS 1,SUPPLIER ADDRESS 2,SUPPLIER ADDRESS 3,SUPPLIER STATE,'   CONSTANT
       HDG2   = 'POST CODE,EFFECTIVE DATE,EXPIRY DATE,ITEM NO,STOCK CODE,ITEM,ITEM DESCRIPTION 1,ITEM DESCRIPTION 2,'  CONSTANT
       HDG3   = 'ITEM DESCRIPTION 3,ITEM DESCRIPTION 4,FREIGHT CODE,DELIVERY CODE,QUANTITY TENDERED,REORDER QUANTITY,' CONSTANT
       HDG4	  = 'UOI,GROSS UNIT PRICE,DISCOUNT,LEAD TIME,STANDARD PACK,UOP,CONVERSION FACTOR,MNEMONIC,PART NO'         CONSTANT
       N      = ','  CONSTANT
       O      = '"'  CONSTANT
       DUMMY  = 'Y'  CONSTANT
       DUM    = CHOOSE ('Y' IF (C = C), DUMMY)
       A = FPA-GROUP
       B = FPA-AGREEMENT
       C = FREIGHT-CODE
       DL = DELIV-LOCATION
       E1 = SUPPLIER-NO
       E  = CATENATE (''',E1) 
       F1 = FPA-ITEM-NO
       F  = CATENATE (''',F1)
       G1 = STOCK-CODE
       G  = CATENATE (''',G1)
       H = GROSS-PRICE-P  D-PIC(-ZZZZZZZZZZ9.9999)
       I = DISCOUNT-A     D-PIC(-ZZ9.99)
       J = SUPP-LEAD-TIME
       K = STD-PACK
       L = UNIT-OF-PURCH
       M = CONV-FACTOR    D-PIC(-ZZZZZZ9.9999)
       PO1 = PURCH-OFFICER
       PO  = CATENATE (''',PO1)
       MNEM = MAN-MNEMONIC-A
       PART1 = MAN-PART-A
       PART2 = CATENATE (''',PART1)
       PART = CHOOSE (PART2 IF (PART1 <> SPACES), SPACES)  
       EF = GET (DISPLAY-DATE FROM (EFFECTIVE-DATE))
       EX = GET (DISPLAY-DATE FROM (EXPIRY-DATE))
%* msf250
       FPA-DESCR = FPA-DESC OF MSF250 -
                 WHERE (FPA-REC-TYPE = %FRT% , FPA-GROUP = %FPA%)
%* msf200
       P = SUPPLIER-NAME OF MSF200 -
                 WHERE (SUPPLIER-NO = SUPPLIER-NO)
       Q = ORDER-ADDR-1 VIA (P)
       R = ORDER-ADDR-2 VIA (P)
       S = ORDER-ADDR-3 VIA (P)
       SS = ORDER-STATE VIA (P)
       PC = ORDER-PCODE VIA (P)
%* msf100
       X = ITEM-NAME OF MSF100 -
                 WHERE (STOCK-CODE = STOCK-CODE)
       Y = UNIT-OF-ISSUE VIA (X)
       STK-DESC1 = DESC-LINE-1 VIA (X)
       STK-DESC2 = DESC-LINE-2 VIA (X)
       STK-DESC3 = DESC-LINE-3 VIA (X)
       STK-DESC4 = DESC-LINE-4 VIA (X)
%* msf251
       Z = QTY-TEND-UOI OF MSF251 -
                 WHERE(FPA-REC-TYPE = %FRT%, -
                       FPA-GROUP    = %FPA%, -
                       FPA-ITEM-NO  = FPA-ITEM-NO, -
                       DSTRCT-CODE  = DSTRCT-CODE)   D-PIC(-ZZZZZZZZZZ9.99)
       RQ = NORM-ORD-QTY-I VIA (Z)
%* msf810
       FRSN = FIRST-NAME OF MSF810 -
                 WHERE(EMPLOYEE-ID = PURCH-OFFICER)
       SECN = SECOND-NAME VIA (FRSN)
       SURN = SURNAME VIA (FRSN)
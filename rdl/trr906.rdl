%*** TRR906.RDL
%***
%*** Copyright (c) 1990, Mincom Pty Ltd, Brisbane 4120, Australia.
%*** All rights reserved.
%***
%*** Revision History:
%*** 20-Dec-2012  a9jw6280       MO164 ---
%*** ......................      Changed format numeric value into -,---,---,---,---.99
%*** 27-Nov-12    a9jw6280       MO164 ---
%*** ......................      Rewrite logic and layout of the program to have
%*** ......................      summary/detail level.
%*** ......................      TRR906 now has 2 reports, TRR906A for sumary
%*** ......................      and TRR906B for detail.
%*** 05-Sep-12    N Perry        MO164
%*** ......................      Reset version number to 0001 for E8 upgrade
%*** 27-Jul-12    N Perry        MO164
%*** ......................      Convert to Ellipse 8 format.
%*** 08-Dec-2000  E Fredeluces   W00120189
%***                             Added the amount column.
%*** 05-Apr-2000  E Fredeluces   W00040054
%***                             Initial Coding.
%***
%PROGRAM-ID
       TRR906
%AUTHOR
       Elizabeth Fredeluces
%NARRATIVE
      **  This will generate Unapproved Manual Journals Report.
%VERSION
       0001
%MODULE
       3901
%FILES
        MFILE = MSF900C
        LFILE = MSF917
        LFILE = MSF620
        RFILE = TRR906A
        RFILE = TRR906B
%SELECTION
        WHERE    (DSTRCT-CODE-4   = %DISTRICT-CODE%, -
                  FULL-PERIOD-4  >= PRD, -
                  FULL-PERIOD-4  <= PRD)
        REJECTIF (REC900-TYPE    <> 'M')
        REJECTIF (MANJNL-VCHR-M  <  %JFROM%,   %JFROM% <> SPACES)
        REJECTIF (MANJNL-VCHR-M   > %JTO%,     %JTO%   <> SPACES)
        REJECTIF (REVERSAL-IND-M <> %ACCR%,    %ACCR%  <> SPACES)
        REJECTIF (ACCOUNTANT-M   <> %ACCNTN%, %ACCNTN% <> SPACES)
        SELECTIF (POSTED-STATUS   = 'B', %STATUS% = 'P')
        SELECTIF (POSTED-STATUS   = 'C', %STATUS% = 'P')
        SELECTIF (POSTED-STATUS   = 'G', %STATUS% = 'P')
        SELECTIF (POSTED-STATUS   = 'T', %STATUS% = 'P')
        SELECTIF (POSTED-STATUS   = 'N', %STATUS% = 'A')
        SELECTIF (POSTED-STATUS   = 'O', %STATUS% = 'A')
        SELECTIF (POSTED-STATUS   = 'U', %STATUS% = 'U')
        SELECTIF (POSTED-STATUS   = 'V', %STATUS% = 'U')
%ORDER
       JRNL
       JNL
       ACCTCD       
%BREAK-FIELDS
       JRNL
       JNL
%REPORT
       A       
%TITLE
'List of Manual Journals Entered - Summary'
%PAGE HEADING
<IF %SORD% = 'S'>
       'Accrual  Accounting'                                                                                'Debit           Credit'
'Journal   Flag  Period   Trans Date Journal Description                 Accountant  Status                 Amount           Amount'
<ENDIF>
%BREAK JRNL FOOTING
<IF JRNL <> SPACES>
 <IF %SORD% = 'S'>
 {JRNL    }  R   {PER   } {TRDATE}   {JRNLDESC                        } {ACCT    }  {STATDESC}  {DRAMT          } {CRAMT          }
 <ENDIF>
<ENDIF>
%SUMMARY
<IF %SORD% = 'S'>
'__________________________________________________________________________________________________________________________________'


'                                                      ***  End of Report  ***'
<ENDIF>
%REPORT
       B       
%TITLE
'List of Manual Journals Entered - Detail'
%PAGE HEADING
%BREAK JRNL HEADING ON NEW-PAGE
<IF JRNL <> SPACES>
 <IF %SORD% = 'D'>
'                                                                                                                                   ' 
'Journal No          : '{JRNL    }                                                                  'Accounting Period : '{APRD  }
'Journal Description : '{JRNLDESC                                }                                  'Trans Date        : '{TRDATE}
'Status              : '{STATDESC}                                                                  'Accountant        : '{ACCT    }
'                                                                                                                                   '
             'Accrual'                                                                                     'Debit             Credit'
'Account Code  Flag   Project  Work Order Journal Line Description                                         Amount             Amount'
'___________________________________________________________________________________________________________________________________'
'                                                                                                                                   '
 <ENDIF>
<ENDIF>
%DETAIL
<IF %SORD% = 'D'>
 {ACCTCD    }   R     {PROJ  } {WO    }   {JRNLLNDESC                                 }         {DAMT           }  {CAMT           }
<ENDIF> 
%BREAK JRNL FOOTING
<IF JRNL <> SPACES>
 <IF %SORD% = 'D'>
                                                                                                '__________________________________'
                                                                                                '                                  ' 
'Journal Total :'                                                                               {DRAMT          }  {CRAMT          }
                                                                                                '__________________________________'
 <ENDIF>
<ENDIF>
%SUMMARY
<IF %SORD% = 'D'>
'__________________________________________________________________________________________________________________________________'


'                                                      ***  End of Report  ***'
<ENDIF>
%REQUEST-PARAMETERS
       AP     D-PIC (X(4))  TEXT  'Accounting Period (yymm)  : '
       JFROM  D-PIC (X(10)) TEXT  'Journal No. From          : '
       JTO    D-PIC (X(10)) TEXT  'Journal No. To            : '
       STATUS D-PIC (X(1))  TEXT  'Status (A/U/P)            : '
       ACCR   D-PIC (X(1))  TEXT  'Accrual                   : '
       ACCNTN D-PIC (X(10)) TEXT  'Accountant                : '
       SORD   D-PIC (X(1))  TEXT  'Summary/Detail            : '
%VARIABLES
      AP         = %AP%                               CONSTANT
      APYR       = substr (AP,3,2)                    CONSTANT
      APMO       = substr (AP,1,2)                    CONSTANT
      CY         = choose ('19' IF (APYR > 99), '20') CONSTANT
      PRD        = catenate (CY,APYR,APMO)            CONSTANT
      APRD       = catenate (APMO,'/',APYR)           CONSTANT
      AMT        = TRAN-AMOUNT
      DAMT       = CHOOSE (AMT IF (AMT > 0), 0) D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
      DRAMT      = TOTAL  (DAMT)   D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
      CAMT       = CHOOSE (AMT IF (AMT < 0), 0) D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
      CRAMT      = TOTAL  (CAMT)   D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
      TRDATE     = TRNDTE-REV-4-9 FORMAT (REVERSE-DATE)
      JNL        = MANJNL-VCHR-M
      JRNL       = choose (JNL IF (JNL <> SPACES), JNL)
      JRNLLNDESC = JOURNAL-DESC-M
      S          = POSTED-STATUS
      STATDESC   = choose ('Unapproved' IF ((S = 'U') OR (S = 'V')), -
                           'Approved'   IF ((S = 'N') OR (S = 'O')), -
                           'Posted'     IF ((S = 'B') OR (S = 'C') OR (S = 'G') OR (S = 'T')))

     ACCT        = ACCOUNTANT-M
     ACCTCD      = ACCOUNT-CODE
     PER         = FULL-PERIOD
     PROJM       = PROJECT-NO-M
     WO          = WORK-ORDER-M
     PROJW       = PROJECT-NO OF MSF620 -
                   WHERE (DSTRCT-CODE = %DISTRICT-CODE%,-
                          WORK-ORDER  = WO)
     PROJ        = CHOOSE (PROJM IF (PROJM <> SPACES), PROJW)                      
     R           = REVERSAL-IND-M
     TCAMT       = TOTAL (CAMT) D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
     TDAMT       = TOTAL (DAMT) D-PIC (SZ,ZZZ,ZZZ,ZZZ,ZZ9.99)
     JRNLDESC    = JNL-DESC OF MSF917 -
                   WHERE (DSTRCT-CODE = DSTRCT-CODE, -
                          JOURNAL-NO  = MANJNL-VCHR-M)

%*
%*     *** End of Program ***
%*

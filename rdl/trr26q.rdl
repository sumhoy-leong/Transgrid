%*** TRR26Q.rdl
%*** Revision History ***
%*** 08-Mar-13  a9nh4814   MO164 (Version 0002)
%*** ..................... Changed invoice date filtering
%*** 31-Oct-12  a9jw6280   MO164 ---
%*** ..................... Format price into 2 digits decimal rather than 4 digits.
%*** 30-Jul-12  a9jw6280   MO164 
%*** ..................... Extend the length of numeric variables to 
%*** ..................... accomodate 17 digits.
%*** 30-Jul-12  a9jw6280   MO164 (Version 0001)
%*** ..................... Added spaces between operands and operator to avoid
%*** ..................... RDL runner wrongly recognize it as variable.
%*** ..................... e.g. L / 100 NOT L/100
%*** ..................... Reset version number to 0001 for E8 upgrade.
%*** 14-Mar-12  GPresser  3897518                (Version 006)
%*** .................... Only select 'O'wned Stock or 'P'urchase Req items
%*** 01-Jul-05 Amanda N.     P05060270
%*** ......................  - Modified to include warehouse code e.g '-N1'
%*** ......................  as a suffix to PR for district 'GRID'.
%*** ......................  - Added Warehouse Request Parameter.
%*** ......................  - Added Warehouse description heading if entered.
%*** 27-Oct-04 RYeung     WCHG070489
%*** .................... Include all invoices of status 19 in weekly Order.
%*** 12-Oct-04 RYeung     WCHG070489
%*** .................... Include all invoices of status 19.
%*** 02-Jan-03 RK Mahajan WCHG012118 - Added new parameters for filtering.
%***                      Changed version no from 004A to 005.
%*** 04-Sep-01 RK Mahajan W01081066
%*** 04-Nov-98 Brandon    Fix for MIMS 4.3
%*** 25-SEP-96 Mincom LC  Replace HRGET macro with direct read of msf810.
%*** 16-Apr-96 Mincom     Replace 'RC' table access with HRGET macro.
%*** 03-May-95 Assia_S    FRS M-558101
%*** .................... Added Accountant descriptor showing name and service
%*** .................... number.
%*** 13-Sep-93 David_G    Ver 004A Provide invoice-date range filter.
%*** 31-Aug-92 David_G    sort by district with page breaks for pacific power
%*** 02-Aug-91 Philr      C42357 MINC1 ---
%*** .................... REPORT A MISMATCH WHEN ALL INV ITEMS MATCH (IE ARE
%*** .................... APPROVED STATUS '30') BUT THE TOTAL INVOICE VALUE
%*** .................... DOESN'T MATCH WITH ORDER.
%*** 02-Aug-91 Peters     C98001 Version 003C. 3.008A to 3.008B.
%*** 18-Jul-91 Philr      C43257 MINC1 ---
%*** .................... NOT ALWAYS REPORTING TAX ON ORDER CORRECTLY.
%*** 18-Jul-91 Philr      C43257 Version 003B. 3.007 to 3.008A.
%*** 08-Mar-91 Philr      C31526 MINC4 --- CATER FOR SALES TAX PAYABLE AT TIME
%*** .................... OF ISSUE.
%*** 08-Mar-91 Philr      C31526 Version 003A. 3.005 to 3.006B.
%*** 04-Oct-90 Barbara    Change to use AFILE to make multi district processing
%*** .................... more efficient.
%*** 04-Sep-90 Philr      C19254 Version 003. 3.004 to 3.005/4.
%*** 15-AUG-90 Pauld      Reject MSF260 records if NRPT-267-DATE > current-date
%*** 22-Jul-90 Ross       Change code for FPA file changes
%*** 22-Jul-90 Ross       CSB26B Version 002. 3.001B to 3.004/2.
%*** 06-Apr-90 PHILR        BREAK ON CHANGE OF DISTRICT
%*** 05-Apr-90 Philr      CSB26B Version 001B. From M3000C to M3001A.
%*** 21-Mar-90 Ross       C99548 Version 001A. From M3000 to M3000C.
%*** 08-Dec-89 PHILR   Initial Coding
%***                  Generated by Version 001 of NEWRDL.MAC
%***
%PROGRAM-ID
       TRR26Q
%AUTHOR
       David Gatward
%NARRATIVE
      **  Report of invoices that have mismatched due to price.
%VERSION
       0002
%MODULE
       3260
%ROUTINES
       MSS232
       MSSDAT
       MSSCNV
%FILES
       MFILE = MSF000
       AFILE = MSF260A
       AFILE = MSF26A
       LFILE = MSF010
       LFILE = MSF100
       LFILE = MSF200
       LFILE = MSF220
       LFILE = MSF221
       LFILE = MSF231
       LFILE = MSF261
       LFILE = MSF810
       RFILE = TRR26QA
%SELECTION
<IF %DISTRICT-CODE% = SPACES>
  WHERE    (DSTRCT-CODE      = SPACES, -
            CONTROL-REC-TYPE = 'AD', -
            CONTROL-REC-NO  >= SPACES)
  SELECTIF (DSTRCT-STATUS    = 'A')
<ELSE>
  WHERE    (DSTRCT-CODE      = SPACES, -
            CONTROL-REC-TYPE = 'AD', -
            CONTROL-REC-NO  >= %DISTRICT-CODE%)
  SELECTIF (DSTRCT-STATUS    = 'A')
  STOPIF   (CONTROL-REC-NO   > %DISTRICT-CODE%)
<ENDIF>
%ORDER
%* Sort on PMT-STATUS-2 , should always be 'ZZ' for items
%* This temporarily overcomes a bug to force MSF260A to be read before
%* MSF220.
   ASC CONTROL-REC-NO
       PMT-PERIOD            VIA (SNO)
       ITM-PMT-STATUS        VIA (K)
       PURCH-OFFICER         VIA (P-OFFCR)
       SUPPLIER-NO           VIA (SNO)
       INV-NO                VIA (SNO)
       INV-ITEM-NO           VIA (K)
%CONTROL-BREAKS
       CONTROL-REC-NO
       PMT-PERIOD            VIA (SNO)
       ITM-PMT-STATUS        VIA (K)
       PURCH-OFFICER         VIA (P-OFFCR)
       SUPPLIER-NO           VIA (SNO)
       INV-NO                VIA (SNO)
%**CONTROL
%**       INCLUDE-EMPTY-SUMMARY
%REPORT-TITLE
       'INVOICES MISMATCHED DUE TO A PRICE DISCREPANCY'
%PAGE HEADING
<IF %WHOUSE% <> SPACES>
                                                 {WHDESC                                      }
<ENDIF>

                        'Exemption Report - Invoice date From : ' {DATEFR} ' to ' {DATETO} ' - ' {OP-LIT   }

<IF PRTITM = 'Y' AND P-OFFCR <> SPACES>
'Purchasing Officer :' {P-OFFCR } {P-OFFCR-NAME                          }
'District           :' {DS}       {DSNAME                                }
'Supplier           :' {SNO }     {SUP-PMT-NAME                  }'Phone :'{SUP-PMT-PHONE } 'Currency :'{CT}

<ENDIF>
%DETAIL ON NEW-PAGE
<IF PRTITM = 'Y' AND PRTDTE = 'Y'>
    <IF ORDER-TYPE = 'O'>
'Stock Code         :' BBBBBBBBB 'Description :' {STOCK-DESC                              }
    <ELSE>
    <IF ORDER-TYPE = 'P' AND PR-STOCK-CODE = '         '>
'Purchase Req No    :' CCCCCC    'Item        :' DDD 'Description :'{PREQ-DESC                             }
    <ENDIF>
    <ENDIF>
    <IF FPA <> '      '>
'FPA Number         :' {FPA }
    <ELSE>
'FPA Number         :  N/A'
    <ENDIF>

'---------------------------- ORDER -----------------------------     ------------------------- INVOICE -------------------------'

    <IF LST-AMD-NO > 0>
                                                                     'Accountant'{ACC     }{ACCDES                               }  
  'Order/Item'  {POWH   }'/'GGG                                      'Invoice/Item'JJJJJJJJJJJJJJJJJJJJ'/'KKK 
                                   'Quantity'          {PO-QTY    }                                 'Quantity    '    {INV-QTY-M }
  'Order Date'      {O-DAT-D}      'Price'         {PO-PRICE      }  'Invoice Date'     {I-DAT-D} 'Price       ' {INV-PRICE-M    }
  'UOP'                  IIII      'Disc/Sur'      {PO-DISC-AMT   }  'Date Changed'     {M-DAT-D} 'Discount    ' {INV-DISC-M     }
  'Disc/Sur 1 %'      LLLLLLL      'Sales Tax'     {PO-STAX-AMT   }                               'Sales Tax   ' {INV-STAX-M     }
  'Disc/Sur 2 %'      MMMMMMM
  'Sales Tax  %'      NNNNNNN      'Nett Price  '  {O-NET-PRC     }                               'Nett Price  ' {INV-NETT-M     }


  'Total Price'{PO-VALUE      }                                      'Total Price ' {INV-VALUE-M    }
  'Disc/Sur'   {PO-T-DISC     }                                      'Disc/Sur    ' {INV-T-DISC-M   }
                                                                                                    
  'Value'      {PO-NET-VAL    }                                      'Value       ' {I-NET-V-M      }
                                                                                                    
  'Tax'        {PO-T-TAX      }                                      'Tax         ' {INV-T-TAX-M    }
                                                                                                    
  'Total'      {PO-TN-VAL     }                                      'Total       ' {INV-TN-VAL-M   }
                                                                                                    
                                                                  '** Variance    ' {VARIANCE-M     }
    <ELSE>

  'Order/Item'   {POWH   }'/'GGG                                     'Invoice/Item'JJJJJJJJJJJJJJJJJJJJ'/'KKK
                                   'Quantity'          {PO-QTY    }                               'Quantity    '      {INV-QTY-O }
  'Order Date'      {O-DAT-D}      'Price'         {PO-PRICE      }  'Invoice Date'     {I-DAT-D} 'Price       ' {INV-PRICE-O    }
  'UOP'                  IIII      'Disc/Sur'      {PO-DISC-AMT   }                               'Discount    ' {INV-DISC-O     }
  'Disc/Sur 1 %'      LLLLLLL      'Sales Tax'     {PO-STAX-AMT   }                               'Sales Tax   ' {INV-STAX-O     }
  'Disc/Sur 2 %'      MMMMMMM                                                                                                   
  'Sales Tax  %'      NNNNNNN      'Nett Price  '  {O-NET-PRC     }                               'Nett Price  ' {INV-NETT-O     }


  'Total Price'{PO-VALUE      }                                      'Total Price ' {INV-VALUE-O    }
  'Disc/Sur'   {PO-T-DISC     }                                      'Disc/Sur    ' {INV-T-DISC-O   }
                                                                                                    
  'Value'      {PO-NET-VAL    }                                      'Value       ' {I-NET-V-O      }
                                                                                                    
  'Tax'        {PO-T-TAX      }                                      'Tax         ' {INV-T-TAX-O    }
                                                                                                    
  'Total'      {PO-TN-VAL     }                                      'Total       ' {INV-TN-VAL-O   }

                                                                  '** Variance    ' {VARIANCE-O     }
    <ENDIF>
                                                                  '** Error       ' {ERROR-MSG                                   }

'----------------------------------------------------------------       ---------------------------------------------------------'
'| Supplier |  Order / Item | New Price | Disc 1 | Disc 2 | Tax |       |              Invoice / Item | Amount | Discount | Tax |'
'|          |               |           |        |        |     |       |                             |        |          |     |'
'|' {SNO } '|'FFFFFF'/' GGG'|           |        |        |     |       |'JJJJJJJJJJJJJJJJJJJJ'/' KKK'|        |          |     |'
'----------------------------------------------------------------       ---------------------------------------------------------'



'Amended By: _____________________________________ On: __________     Approved By: _______________________________ On: __________'


'Comments  :'
<ENDIF>
%BREAK INV-NO VIA (SNO) FOOTING ON NEW-PAGE SUPPRESS-PAGE-HEADING
<IF (HSTAT = '17' AND IMMCNT = 0 AND PRTDTE = 'Y')  OR  HSTAT = '19'>

'Purchasing Officer :' {HPOFF   } {HPOFFNAME                             }
'District           :' {DS}       {DSNAME                                }
'Supplier           :' {SNO }     {SUP-PMT-NAME                  }'Phone :'{SUP-PMT-PHONE } 'Currency :'{CT}

'----------------------------------------------------------------------------------------------------------------------------------'

'---------------------------- ORDER -----------------------------     ------------------------- INVOICE -------------------------'

  'Order'           FFFFFF                                           'Invoice'            JJJJJJJJJJJJJJJJJJJJ
  'Order Date'      {O-DAT-D}                                        'Invoice Date'       {I-DAT-D}

  'Value'      {TOPRC         }                                      'Value       ' {TIPRC          }
  'Tax'        {TOTAX         }                                      'Tax         ' {TITAX          }
                                                                                                
  'Total'      {TOVAL         }                                      'Total       ' {TIVAL          }

                                                                  '** Variance    ' {TVAR           }
                                                                  '** Error       ' {TERRMSG                                     }

                                                                  '** TOTAL value of invoice DOES NOT match with order'





'Amended By: _____________________________________ On: __________     Approved By: _______________________________ On: __________'


'Comments  :'
<ENDIF>
%REPORT-SUMMARY

'          **************  End  of  the  TRR26Q  Report  *************'
%REQUEST-PARAMETERS
    RUN-NO   PIC (X(6)) TEXT 'Run No                               :'
    FROM-DTE PIC (X(8)) TEXT 'From Invoice Date                    :'
    TO-DTE   PIC (X(8)) TEXT 'To   Invoice Date                    :'
    DWMCYCLE PIC (X(1)) TEXT 'Daily/Weekly/Monthly-for blank dates :'
    WHOUSE   PIC (X(2)) TEXT 'Warehouse Code                       :'
    OWNPUR   PIC (X(1)) TEXT 'Owned Stock/Purchase Req      O/P    :'
%VARIABLES
%* Constants
FDTE    = %FROM-DTE%                      CONSTANT FORMAT (DATE)
TDTE    = %TO-DTE%                        CONSTANT FORMAT (DATE)
DWM     = %DWMCYCLE%                      CONSTANT
OP      = %OWNPUR%                        CONSTANT
DS      = CONTROL-REC-NO
DSNAME  = GET (DISTRICT-NAME FROM ('MSS002') DISTRICT (DS))
OP-LIT   = CHOOSE('Owned Stock' IF (OP = 'O'), -
                  'Purch Req  ' IF (OP = 'P'), SPACES)
%*
%* Calculate Daily/Weekly/Monthly date in case CutOff date is blank.
%* For Daily flag date from and to both will be YESTERDAY.
    TODAY   = DATE ()                     CONSTANT FORMAT (DATE)
    YESTDAY = DATE2 OF MSSDAT -
              WHERE (OPTION = '1', -
                     DATE1  = TODAY, -
                     DAYS   = -1)         CONSTANT FORMAT (DATE)
%* For Weekly flag date from = 7 days before today
%* and date to = YESTERDAY.
    W1      = DATE2 OF MSSDAT -
              WHERE (OPTION = '1', -
                     DATE1  = TODAY, -
                     DAYS   = -7)         CONSTANT FORMAT (DATE)
%* For Monthly flag dates are last period start and end dates.
    CP1     = WX-STORES-CP                CONSTANT
    LP      = PERIOD-YRMN OF MSSDAT -
              WHERE (OPTION      = 'J', -
                     PERIOD-YRMN = CP1, -
                     DAYS        = -1)    CONSTANT
    STDATE  = STARTING-DATE-1-9  VIA (LP) CONSTANT FORMAT (DATE)
    ENDDATE = ENDING-DATE-1-9    VIA (LP) CONSTANT FORMAT (DATE)
%*
%* choose date FROM and TO based on the flag when dates are blank.
    DATEFR  = CHOOSE (FDTE    IF (FDTE <> SPACES), -
                      YESTDAY IF (FDTE  = SPACES, DWM = 'D'), -
                      W1      IF (FDTE  = SPACES, DWM = 'W'), -
                      STDATE  IF (FDTE  = SPACES, DWM = 'M'), -
                      SPACES)
    DATETO  = CHOOSE (TDTE    IF (TDTE <> SPACES), -
                      YESTDAY IF (TDTE  = SPACES, DWM = 'D'), -
                      YESTDAY IF (TDTE  = SPACES, DWM = 'W'), -
                      ENDDATE IF (TDTE  = SPACES, DWM = 'M'), -
                      SPACES)

    BLANKDATE = CHOOSE ('Y' IF (FDTE = SPACES AND TDTE = SPACES), 'N')
%*
SNO     = SUPPLIER-NO FROM MSF260A -
          WHERE    (INV-REC-TYPE   = 'H', -
                    DSTRCT-CODE-2  = CONTROL-REC-NO, -
                    APPR-STATUS    = '00', -
                    SUPPLIER-NO-2 >= SPACES) -
          REJECTIF (INV-TYPE      <> '0',-
                    INV-TYPE      <> '1')
INVDTE = INV-DATE                      VIA (SNO) FORMAT (DATE)
%*
SUPP-P-NAME   = PAYMENT-NAME       OF MSF200 -
                                   WHERE    (SUPPLIER-NO  = SNO)
EXTINV = EXT-INV-NO VIA (SNO)
%*
SUPP-O-NAME   = SUPPLIER-NAME      VIA (SUPP-P-NAME)
SUP-PMT-NAME  = CHOOSE(SUPP-P-NAME IF (SUPP-P-NAME <> SPACES),SUPP-O-NAME)
SUP-PMT-PHONE = PAYMENT-PHONE      VIA (SUPP-P-NAME)
HSTAT         = PMT-STATUS         VIA (SNO)
INO           = INV-NO             VIA (SNO)
HPONO         = PO-NO              VIA (SNO)
%*
HPOFF         = PURCH-OFFICER      OF MSF220 -
                                   WHERE    (PO-NO = HPONO)
%*
       HPOFFNAM1 = SURNAME OF MSF810 WHERE(EMPLOYEE-ID = HPOFF)
       HPOFFNAM11 = FIRST-INITIAL VIA(HPOFFNAM1)
       HPOFFNAME  = CATENATE(HPOFFNAM11,' ',HPOFFNAM1)
%*
K             = INV-ITEM-NO        FROM MSF26A -
                                   WHERE    (DSTRCT-CODE  = CONTROL-REC-NO,-
                                             SUPPLIER-NO  = SNO,-
                                             INV-NO       = INO,-
                                             INV-ITEM-NO >= '001')
%*
ACC           = ACCOUNTANT         VIA (SNO)
ISTAT         = ITM-PMT-STATUS     VIA (K)
NRPDAT        = NRPT-267-DATE      VIA (K)

%*PRTDTE        = CHOOSE('Y' IF (INVDTE >= DATEFR, INVDTE <= DATETO), -
%*                       'Y' IF (ISTAT = '19', DWM = 'W'),            -
%*                       'N')

IMMIND        = CHOOSE(1 IF (ISTAT = '17'),-
                       1 IF (ISTAT = '19'),-
                       0)
IMMCNT        = total(IMMIND)
 
ININV = INV-NO VIA (K)
J = CHOOSE (EXTINV IF (EXTINV <> SPACES), ININV)
F             = PO-NO              VIA (K)
G             = PO-ITEM-NO         VIA (K)
%*
ORDER-TYPE    = PO-ITEM-TYPE       OF MSF221 -
                                   WHERE    (PO-NO        = F,-
                                             PO-ITEM-NO   = G)
WHI    = WHOUSE-ID VIA (ORDER-TYPE)
WHID   = SUBSTR (WHI, 1, 2)
WHFLG  = CHOOSE ('N' IF (%WHOUSE% <> SPACES AND %WHOUSE% <> WHID), 'Y')
WH     = CATENATE (F, '-', WHID)
POWH   = CHOOSE (WH IF (DS = 'GRID'), -
                 F)
ITEM-TYPE = PO-ITEM-TYPE VIA (ORDER-TYPE)
OP-OK     = CHOOSE('Y' IF(ITEM-TYPE = OP), 'N')

PRTITM        = CHOOSE('Y' IF (ISTAT = '17' AND NRPDAT <= WX-TODAYS-DATE AND OP-OK = 'Y'), -
                       'Y' IF (ISTAT = '19' AND NRPDAT <= WX-TODAYS-DATE AND OP-OK = 'Y'), -
                       'N')

PRTDTE        = CHOOSE('Y' IF (INVDTE >= DATEFR, INVDTE <= DATETO, WHFLG = 'Y'),           -
                       'Y' IF (BLANKDATE = 'Y', ISTAT = '19', DWM = 'W', WHFLG = 'Y'),     -
                       'N')

%* Retrieve the Warehouse ID Description
   WHDESC     = TABLE-DESC OF MSF010         -
                WHERE   (TABLE-TYPE = 'WH', -
                         TABLE-CODE =  DS & %WHOUSE%)
%*
%***   LFILE MSF010 : Accountant Description
   
        ACCDESFUL2 = SURNAME OF MSF810 WHERE(EMPLOYEE-ID = ACC)
        ACCDESFUL21 = FIRST-INITIAL VIA(ACCDESFUL2)
        ACCDESFULL = CATENATE(ACCDESFUL21,' ',ACCDESFUL2)
ACCDES     = substr(ACCDESFULL,1,46) 

%*
P-OFFCR       = PURCH-OFFICER      OF MSF220 -
                                   WHERE    (PO-NO        = F)
%*
        P-OFFCR-NAM1 = SURNAME OF MSF810 WHERE(EMPLOYEE-ID = P-OFFCR)
        P-OFFCR-NAM11 = FIRST-INITIAL VIA(P-OFFCR-NAM1)
        P-OFFCR-NAME  = CATENATE(P-OFFCR-NAM11,' ',P-OFFCR-NAM1)
%*
B             = STOCK-CODE         VIA (ORDER-TYPE)
%*
STOCK-DESC    = ITEM-NAME          OF MSF100 -
                                   WHERE    (STOCK-CODE   = B)
%*
C             = PURCH-REQ          VIA (ORDER-TYPE)
D             = PREQ-ITEM          VIA (ORDER-TYPE)
%*
PREQ-DESC     = ITEM-DESC          OF MSF231 -
                                   WHERE    (DSTRCT-CODE  = CONTROL-REC-NO,-
                                             PREQ-NO      = C,-
                                             PREQ-ITEM-NO = D)
%*
PR-STOCK-CODE = STOCK-CODE         VIA (PREQ-DESC)
FPA           = FPA-ID             VIA (ORDER-TYPE)
LST-AMD-NO    = LAST-AMEND-NO-9    VIA (K)
%*
INV-QTY-M     = AMEND-QTY          OF MSF261 -
                                   WHERE    (DSTRCT-CODE  = CONTROL-REC-NO,-
                                             SUPPLIER-NO  = SNO,-
                                             INV-NO       = ININV,-
                                             INV-ITEM-NO  = K,-
                                             AMENDMENT-NO = LST-AMD-NO)
%*
INV-QTY-O     = QTY-INVOICED       VIA (K)
O-DAT-F       = ORDER-DATE         VIA (P-OFFCR)
O-DAT-D       = GET (DISPLAY-DATE FROM (O-DAT-F))
I             = UNIT-OF-PURCH      VIA (K)
PO-QTY        = choose(INV-QTY-M IF (LST-AMD-NO > 0),INV-QTY-O)
PO-PRICE      = GROSS-PRICE-P      VIA (ORDER-TYPE) D-PIC SZ(12)9.99
CNETPRC       = CURR-NET-PR-P      VIA (ORDER-TYPE)
L             = DISCOUNT-A         VIA (ORDER-TYPE) I-PIC S999V99
M             = DISCOUNT-B         VIA (ORDER-TYPE)
N             = TAX-PERCENT        VIA (ORDER-TYPE)
TCODE         = TAX-CODE           VIA (ORDER-TYPE)
%*
TCASVAL       = ASSOC-REC          OF MSF010 -
                                   WHERE    (TABLE-TYPE = 'TC',-
                                             TABLE-CODE = TCODE)
%*
L100          = calc (L / 100)
TISS          = substr(TCASVAL,1,1)
TINPRC        = substr(TCASVAL,2,1)
DISC1-AMT     = calc(PO-PRICE * (L100)) I-PIC SZ(12)9.99
DISC2-AMT     = calc((PO-PRICE + DISC1-AMT) * (M / 100)) I-PIC SZ(12)9.99
PO-DISC-AMT   = calc(DISC1-AMT + DISC2-AMT) I-PIC SZ(12)9.99
TAX-AMT       = calc((CNETPRC) * (N / 100)) I-PIC SZ(12)9.99
PO-STAX-AMT   = choose(0 IF (TISS < '4'),-
                       0 IF (TINPRC = 'Y'),-
                       TAX-AMT)
O-NET-PRC     = calc(PO-PRICE + DISC1-AMT + DISC2-AMT + PO-STAX-AMT) D-PIC SZ(12)9.99
PO-VALUE      = calc(PO-QTY * PO-PRICE) I-PIC SZ(12)9.99
PO-T-DISC     = calc(PO-DISC-AMT * PO-QTY) I-PIC SZ(12)9.99
PO-NET-VAL    = calc(PO-VALUE + PO-T-DISC) I-PIC SZ(12)9.99
PO-T-TAX      = calc(PO-STAX-AMT * PO-QTY) I-PIC SZ(12)9.99
PO-TN-VAL     = calc(PO-VALUE + PO-T-DISC + PO-T-TAX) I-PIC SZ(12)9.99
I-DAT-F       = INV-DATE           VIA (SNO)
I-DAT-D       = GET (DISPLAY-DATE FROM (I-DAT-F))
CT            = CURRENCY-TYPE      VIA (SNO)
INV-VALUE-M   = FOR-AMD-VAL        VIA (INV-QTY-M) D-PIC SZ(12)9.99
M-DAT-F       = AMEND-DATE         VIA (INV-QTY-M)
M-DAT-D       = GET (DISPLAY-DATE FROM (M-DAT-F))
INV-T-TAX-M   = FOR-AMD-STAX       VIA (INV-QTY-M) D-PIC SZ(12)9.99
INV-T-DISC-M  = FOR-AMD-DISC       VIA (INV-QTY-M) 
INV-PRICE-M   = calc (INV-VALUE-M / INV-QTY-M) I-PIC SZ(12)9.99
INV-STAX-M    = calc (INV-T-TAX-M / INV-QTY-M) D-PIC SZ(12)9.99
INV-DISC-M    = calc (INV-T-DISC-M / INV-QTY-M) D-PIC SZ(12)9.99
INV-NETT-M    = calc (INV-PRICE-M + INV-DISC-M + INV-STAX-M)
I-NET-V-M     = calc (INV-VALUE-M + INV-T-DISC-M) I-PIC SZ(12)9.99
INV-TN-VAL-M  = calc(INV-VALUE-M + INV-T-DISC-M + INV-T-TAX-M) I-PIC SZ(12)9.99
VARIANCE-M    = calc(INV-TN-VAL-M - PO-TN-VAL) D-PIC SZZZZZZZZZZZZ9.99
INV-VALUE-O   = FOR-VAL-INVD VIA (K)
INV-PRICE-O   = calc(INV-VALUE-O / INV-QTY-O)
FSI           = FOR-STAX-INVD      VIA (K)
FDI           = FOR-DISC-INVD      VIA (K)
INV-STAX-O    = calc(FSI / INV-QTY-O)
INV-DISC-O    = calc(FDI / INV-QTY-O)
INV-NETT-O    = calc(INV-PRICE-O + INV-DISC-O + INV-STAX-O)
INV-T-DISC-O  = FOR-DISC-INVD      VIA (K)
I-NET-V-O     = calc(INV-VALUE-O + INV-T-DISC-O)
INV-T-TAX-O   = FOR-STAX-INVD      VIA (K)
INV-TN-VAL-O  = calc  (INV-VALUE-O + INV-T-DISC-O + INV-T-TAX-O)
VARIANCE-O    = calc  (INV-TN-VAL-O - PO-TN-VAL) D-PIC SZZZZZZZZZZZZ9.99
ERROR-MSG     = CHOOSE('Mismatch due to sales tax' IF (ISTAT = '17'),-
                       'Mismatch due to price')
IPRC          = CHOOSE(I-NET-V-M IF (LST-AMD-NO > 0),-
                       I-NET-V-O)
ITAX          = CHOOSE(INV-T-TAX-M IF (LST-AMD-NO > 0),-
                       INV-T-TAX-O)
TIPRC         = total(IPRC)
TITAX         = total(ITAX)
TIVAL         = calc(TIPRC + TITAX)
TOPRC         = total(PO-NET-VAL)
TOTAX         = total(PO-T-TAX)
TOVAL         = calc(TOPRC + TOTAX)
TVAR          = calc(TIVAL - TOVAL) D-PIC SZZZZZZZZZZZZ9.99
TERRMSG       = CHOOSE('Mismatch due to sales tax' IF (HSTAT = '17'),-
                       'Mismatch due to price')

%PROGRAM-ID
    TRR90J
%AUTHOR
    Geoff Presser - Mincom Pty Ltd
%NARRATIVE
    This RDL extracts records from the MSF900 file (Record Types 'H', 'I',
    'L', 'P', 'Q' and 'S'). These are then formatted and a report is
    produced. This report is used by the Finance people in trying to
    reconcile the CAI transactions.
%*
%*** Revision History
%*** ----------------
%*** 15-Aug-13 Shally Lee        SC0000004308131 V002
%***                             Fix the "other transactions" record not 
%***                             picked up issue for TRR91JA report.
%*** 05-Sep-12 N Perry
%*** Reset version number to 0001 for E8 upgrade
%*** 27-Jul-12 N Perry       
%*** ......................  Convert to Ellipse 8 format.
%*** 15-Sep-09 Janak Barar       SC1926837
%***                             Stop calculating days within periods -        
%***                             (from to in parameter).          
%*** 18-Nov-05 E Fredeluces      P05110870
%***                             Change record length from 17 to 28.
%***                             Change field size of process date from 4 to 8
%***                             and transaction no. from 8 to 11. 
%*** 06-Mar-02 D Diolaso         WCHG001479
%*** ........................... Addition of 'END OF REPORT' to the last page
%*** 24-Oct-01 D Diolaso         W01090492
%*** ........................... Inclusive parameter periods not to exceed
%*** ........................... one quarter or 93 days
%*** 18-Oct-01 D Diolaso         W01071050
%*** ........................... Modified the process into a 2-step process
%*** ........................... to improve the program efficiency.
%*** ........................... Program usually averages 2 hour to complete.
%*** ........................... The change to 2-step process resulted in
%*** ........................... completion time of 30 mimutes average.
%*** ........................... Following are the programs that comprised
%*** ........................... the whole process:
%*** ...........................    a)  TRR90J - Extract portion and
%*** ...........................    b)  TRR91J - The 'look up' and report
%*** ...........................        generation portion.
%*** 28-Aug-00 E Fredeluces      W00080721
%*** ........................... Not displaying the Manual Journal
%*** ........................... description.
%*** 07-Nov-00 D Diolaso         W00070125            (Vesrion 004)
%*** ........................... Inclusion of 'M' transaction type
%*** 18-Nov-99 G Presser         W99110615            (Version 003)
%*** ........................... Fixup the previous selection changes
%*** ........................... & include an extra line for 'I' items
%*** 25-Oct-99 G Presser         W99100692            (Version 002)
%*** ........................... Only process accounts within the
%*** ........................... specified breakdowns
%*
%FILES
    MFILE = MSF000
    AFILE = MSF900C
    OFILE = TRO90JA
%*
%ROUTINES
        MSSDAT
%VERSION
    002
%*
%SELECTION
    WHERE (DSTRCT-CODE      = SPACES, -
           CONTROL-REC-TYPE = 'AD',   -
           CONTROL-REC-NO  >= SPACES)
    SELECTIF (DSTRCT-STATUS = 'A')
%*
%OUTPUT-FILE
    A
%CONTROL
    COPY-REQUEST  = TRR91J
    RECORD-LENGTH = 28 
%*
%DETAIL
<IF SEL = 'Y'>
{DS}{PD    }{TN       }{US}R
<ENDIF>
%*
%SUMMARY
<IF PROC = 'N'>
{PTEXT          }
<ENDIF>
<IF PROC = 'Y' AND TCT = 0>
{DM             }
<ENDIF>
%PARAMETERS
    RCF     D-PIC (X(3)) TEXT 'R/C  From                 : '
    RCT     D-PIC (X(3)) TEXT 'R/C  To                   : '
    BSF     D-PIC (X(3)) TEXT 'B/S  From                 : '
    BST     D-PIC (X(3)) TEXT 'B/S  To                   : '
    ACF     D-PIC (X(3)) TEXT 'A/C  From                 : '
    ACT     D-PIC (X(3)) TEXT 'A/C  To                   : '
    ELF     D-PIC (X(3)) TEXT 'Elem From                 : '
    ELT     D-PIC (X(3)) TEXT 'Elem To                   : '
    PF      D-PIC (X(4)) TEXT 'Account Period From (MMYY): '
    PT      D-PIC (X(4)) TEXT 'Account Period To   (MMYY): '
%*
%VARIABLES
%*
    RCF  = %RCF%                                              CONSTANT
    RCT  = %RCT%                                              CONSTANT
	BSF  = %BSF%                                              CONSTANT
	BST  = %BST%                                              CONSTANT
    ACF  = %ACF%                                              CONSTANT
    ACT  = %ACT%                                              CONSTANT
    ELF  = %ELF%                                              CONSTANT
    ELT  = %ELT%                                              CONSTANT 

    PF-M = substr(%PF%,1,2)                                   CONSTANT
    PF-Y = substr(%PF%,3,2)                                   CONSTANT
    PF   = CATENATE (PF-Y, PF-M)               CONSTANT
    PT-M = substr(%PT%,1,2)                    CONSTANT
    PT-Y = substr(%PT%,3,2)                    CONSTANT
    PT   = CATENATE (PT-Y, PT-M)               CONSTANT
    PFC  = choose ('20' IF (PF-Y < '25'), '19')               CONSTANT
    PTC  = choose ('20' IF (PT-Y < '25'), '19')               CONSTANT
    PFD  = catenate (PFC,PF-Y,PF-M)                           CONSTANT
    PTD  = catenate (PTC,PT-Y,PT-M)                           CONSTANT
%*  Get the starting date of the 'from' period
    S-DT = STARTING-DATE-1-9 OF MSSDAT -
           WHERE (OPTION      = 'G',   -
                  PERIOD-YRMN = PF)                         CONSTANT

%*  Get the ending date of the 'to' period
    T-DT = ENDING-DATE-1-9   OF MSSDAT -
           WHERE (OPTION      = 'G',   -
                  PERIOD-YRMN = PT)                         CONSTANT
%*  Determine the number of days between S-DT and T-DT
    NDAYS = DAYS OF MSSDAT             -
            WHERE (OPTION = '2',       -
                   DATE1  = S-DT,      -
                   DATE2  = T-DT)                             CONSTANT
%*  Number of days between the 'from' period and the 'to'
%*  period should not exceed the equivalent of one quarter
%*  or 93 days

%*  PROC  = CHOOSE ('N' IF (NDAYS > 93), 'Y')                 CONSTANT
    PROC  = CHOOSE ('Y' IF (NDAYS > 93), 'Y')                 CONSTANT
    PTEXT = 'INVALIDPARAM     '                               CONSTANT
    DM    = 'DUMM             '                               CONSTANT

    RCFF = choose ('000' IF (RCF = SPACES), RCF)              CONSTANT
    RCTT = choose (HIGH-VALUES IF (RCT = SPACES), RCT)        CONSTANT
    BSFF = choose ('000' IF (BSF = SPACES), BSF)              CONSTANT
    BSTT = choose (HIGH-VALUES IF (BST = SPACES), BST)        CONSTANT
    ACFF = choose ('000' IF (ACF = SPACES), ACF)              CONSTANT
    ACTT = choose (HIGH-VALUES IF (ACT = SPACES), ACT)        CONSTANT
    ELFF = choose ('000' IF (ELF = SPACES), ELF)              CONSTANT
    ELTT = choose (HIGH-VALUES IF (ELT = SPACES), ELT)        CONSTANT
    ACCF = catenate (RCFF,BSFF,ACFF,ELFF)                     CONSTANT
    ACCT = catenate (RCTT,BSTT,ACTT,ELTT)                     CONSTANT
    PTM  = substr (PT,1,2)                                    CONSTANT
    PFY  = substr (PF,3,2)                                    CONSTANT
    PTY  = substr (PT,3,2)                                    CONSTANT
%*
    DS   = CONTROL-REC-NO
%*
%*  M900
%*
%* V002 [Start]
    M900 = DSTRCT-CODE FROM MSF900C           -
           WHERE (DSTRCT-CODE-4   = DS,       -
                  FULL-PERIOD-4 >= PFD,       -
                  FULL-PERIOD-4 <= PTD)       - 
           STOPIF (PROC <> 'Y')               -
           STOPIF (DSTRCT-CODE-4 <> DS)       -
           REJECTIF (REC900-TYPE <> 'H',      -
                     REC900-TYPE <> 'I',      -
                     REC900-TYPE <> 'L',      -
                     REC900-TYPE <> 'M',      -
                     REC900-TYPE <> 'P',      -
                     REC900-TYPE <> 'Q',      -
                     REC900-TYPE <> 'S',      -
                     REC900-TYPE <> '1')      -
           REJECTIF (ACCOUNT-CODE-3 < ACCF)   -
           REJECTIF (ACCOUNT-CODE-3 > ACCT)   -
           REJECTIF (ACCOUNT-CODE-3 = SPACES)  
%* V002 [End]
%*
    T    = REC900-TYPE                                        VIA (M900)
    AC   = ACCOUNT-CODE-3                                     VIA (M900)
    AC1  = substr (AC,1,3)
    AC2  = substr (AC,4,3)
    AC3  = substr (AC,7,3)
	AC4  = substr (AC,10,3)
    SEL  = choose ('Y' IF (AC1 >= RCFF, AC1 <= RCTT, -
                           AC2 >= BSFF, AC2 <= BSTT, -
                           AC3 >= ACFF, AC3 <= ACTT, -
                           AC4 >= ELFF, AC4 <= ELTT), 'N') 
    PD   = PROCESS-DATE   VIA (M900)
    TN   = TRANSACTION-NO VIA (M900)
    US   = USERNO         VIA (M900)
    R    = REC900-TYPE    VIA (M900)
    CT   = CHOOSE (1 IF (SEL = 'Y'), 0)
    TCT  = TOTAL (CT)
